
// Names ending with underscores are reserved.
// Names starting with underscore must be ignore/unused so we will never need to
// construct composite names from them. (they will just be varibles)
// Names with 2 or more underscores in the middle, add one underscore.
// Form compound names by joining with double underscore.
// Prefix names with kind indicator.
// Suffix names with number followed by parameters.
//
// # Primitives
// Have no prefix, contain no underscores. Names chosen to avoid conflicts with C/C++
// * cp: code point
// * i32, u32: signed and unsigned int
// * size, offset
// * f32: float
// * f16d16: fixed point
// * str: string
// * bit:  bool
//
// # Tuples
// Have a prefix of `t_` and are followed by type parameters. For example:
// t_2__i__i: is a pair of ints
// This marks them as types and doesn't conflict with any other types because
// their name can't start with a number.
//
// # Prefixes
// The simple name prefixes are:
// * t_ class/struct
// * m_ method
// * fn_ function
// * op_ operator
// * c_ construct
// * new_ new and construct
// * v_ variable (needed to avoid creating a variable that conflicts with another name)
//
// # Combinable prefixes
// Reference and optional types are formed by a sequence of letters in a prefix
// * t: a type that isn't primitive (i.e. most types except primitives will have 't')
// * r: reference (don't need to distinguish class/struct the type name says that)
// * o: optional
//
// Examples:
// * rt_Foo - reference to `Foo`
// * or_i32: optional reference to a variable holding an int
//
// # Generic Type Parameters
// The number of generic is appended after a double underscore (except for none)
// * t_Foo__3 - number of type parameters
//
// # Function Parameter Types
// Number of parameters is appended after double underscore, even for none.

// Then arguments are listed, double underscore, number of segments followed by
// name prefix (run together).  Primitives can be listed in place of the number
// example: f_bar__3_s__0r_system__text__String_Builder__1__0i32__0string
// example: f_bar__3__0i32__0r_system__text__String_Builder__0__string
// f_bar__baz__3__1_i32__3r_system_text__String_Builder
// first parameter doesn't need  double underscore or zero
// additional abbreviations i=i32, u=u32 c=cp, b=bit
public class Emitter
{
    public let package: Package;
    public let resources: system.collections.List<Source_Text>;

    public var type_declarations: mut Source_File_Builder;
    public var function_declarations: mut Source_File_Builder;
    public var class_declarations: mut Source_File_Builder;
    public var global_definitions: mut Source_File_Builder;
    public var definitions: mut Source_File_Builder;

    // TODO compute these in a semantic analysis phase
    public var main_function_name: string;
    public var main_function_return_type: string;
    public var main_function_accepts_console: bool;
    public var main_function_accepts_args: bool;

    public new(package: Package, resources: system.collections.List<Source_Text>)
    {
        self.package = package;
        self.resources = resources;
    }
}

public emit(emitter: mut Emitter) -> string
{
    emitter.type_declarations = new Source_File_Builder();
    emitter.function_declarations = new Source_File_Builder();
    emitter.class_declarations = new Source_File_Builder();
    emitter.global_definitions = new Source_File_Builder();
    emitter.definitions = new Source_File_Builder();
    emitter.main_function_return_type = "";
    emitter.main_function_accepts_console = false;
    emitter.main_function_accepts_args = false;

    emit_preamble(emitter);

    for let compilation_unit: Compilation_Unit in emitter.package.compilation_units
    {
        emit_compilation_unit(emitter, compilation_unit);
    }

    emit_entry_point_adapter(emitter);

    // TODO all of this should be `string.join()`
    // TODO the `string.join()` call should be optimized into existence by the compiler from addition
    let total_size: int
        = byte_length(emitter.type_declarations)
        + byte_length(emitter.function_declarations)
        + byte_length(emitter.class_declarations)
        + byte_length(emitter.global_definitions)
        + byte_length(emitter.definitions);
    let cpp_code: mut system.text.String_Builder = new system.text.String_Builder()/*.with_capacity(total_size)*/;
    cpp_code.Append(to_string(emitter.type_declarations));
    cpp_code.Append(to_string(emitter.function_declarations));
    cpp_code.Append(to_string(emitter.class_declarations));
    cpp_code.Append(to_string(emitter.global_definitions));
    cpp_code.Append(to_string(emitter.definitions));
    return cpp_code.ToString();
}

// TODO implement and use mangle_name(name: Name)
public mangle_name(name: Name) -> string
{
    // TODO what about name.is_special == true?
    let builder: mut system.text.String_Builder = new system.text.String_Builder();
    if name.kind == TypeName
        { builder.Append("t_"); }
    else if name.kind == FunctionName
        { builder.Append("f_"); }
    else if name.kind == VariableName
        { builder.Append("v_"); }

    var first_segment: bool = true;
    for let segment: string in name.segments
    {
        if first_segment
            { first_segment = false; }
        else
            { builder.Append("__"); }

        if not contains_multi_underscore_runs(segment)
            { builder.Append(segment); }
        else
        {
            // Due to limitations with strings and loops, do the equivalent
            // of a for loop
            var underscores: int = 0;
            var i: int = 0;
            while i < segment.ByteLength()
            {
                if segment[i] == '_'
                    { underscores += 1; }
                else if underscores > 0
                {
                    if underscores >= 2
                    {
                        builder.Append("_");
                    }
                    underscores = 0;
                }
                builder.Append(new string(segment[i], 1)); // really bad memory use
                i += 1;
            }
        }
    }
    return builder.ToString();
}

public contains_multi_underscore_runs(value: string) -> bool
{
    // Due to limitations with strings and loops, do the equivalent
    // of a for loop
    var i: int = 0;
    var last_char_underscore: bool = false;
    while i < value.ByteLength()
    {
        if value[i] == '_'
        {
            if last_char_underscore
                { return true; }
            last_char_underscore = true;
        }
        else
            { last_char_underscore = false; }
        i += 1;
    }
    return false;
}

public convert_primitive_type_name(type: Type) -> string
{
    let name: string = unqualified_name(type.name);
    assert(type.is_primitive, name);
    if name == "bool"
    {
        return "bit";
    }
    else if name == "int"
    {
        return "i32";
    }
    else if name == "uint"
    {
        return "u32";
    }
    else if name == "code_point"
    {
        return "cp";
    }
    else if name == "string"
    {
        return "str";
    }
    else if name == "void"
    {
        return "void";
    }
    else
    {
        NOT_IMPLEMENTED(unqualified_name(type.name));
    }
}

// Converts a type to C++
public convert_type_name(type_node: Semantic_Node) -> mut system.text.String_Builder
{
    assert(type_node.referenced_type =/= none, get_text(type_node));
    return convert_type_name(type_node.referenced_type);
}

public convert_type_name(type: Type) -> mut system.text.String_Builder
{
    let cpp_type: mut system.text.String_Builder = new system.text.String_Builder();
    if type.is_primitive
    {
        cpp_type.Append(convert_primitive_type_name(type));
    }
    else
    {
        cpp_type.Append(mangle_name(type.name));
    }

    if |type.type_parameters| > 0
    {
        cpp_type.Append("<");
        var first: bool = true;
        for let type_parameter: Type in type.type_parameters
        {
            if first { first = false; }
            else { cpp_type.Append(", "); }
            cpp_type.Append(convert_type(true, type_parameter, false));
        }
        cpp_type.Append(">");
    }
    return cpp_type;
}

public convert_reference_type(mutable_binding: bool, var type: Type, nullable: bool) -> string
{
    let cpp_type: mut system.text.String_Builder = convert_type_name(type);
    if not type.is_mutable { cpp_type.Append(" const"); }
    cpp_type.Append(" *");
    if nullable
        { cpp_type.Append("_Nullable"); }
    else
        { cpp_type.Append("_Nonnull"); }
    if not mutable_binding { cpp_type.Append(" const") ; }
    return cpp_type.ToString();
}

// Converts a type to C++
public convert_type(mutable_binding: bool, var type: Type, optional: bool) -> string
{
    assert(type =/= none, "");

    if type.is_primitive and unqualified_name(type.name) == "optional"
    {
        let optional_type: Type = type.type_parameters[0];
        if optional_type.is_value_type
        {
            // optional<T> is a const struct
            let cpp_type: mut system.text.String_Builder = new system.text.String_Builder("p_optional<");
            cpp_type.Append(convert_type(true, optional_type, true));
            cpp_type.Append(">");
            if not mutable_binding and not type.is_mutable
                { cpp_type.Append(" const"); }
            return  cpp_type.ToString();
        }
        else
        {
            return convert_type(mutable_binding, optional_type, true);
        }
    }
    else
    {
        if type.is_value_type
        {
            let cpp_type: mut system.text.String_Builder = convert_type_name(type);
            if not mutable_binding and not type.is_mutable
                { cpp_type.Append(" const"); }
            return cpp_type.ToString();
        }
        else
        {
            return convert_reference_type(mutable_binding, type, optional);
        }
    }
}

public convert_type(mutable_binding: bool, type_node: Semantic_Node) -> string
{
    assert(type_node.referenced_type =/= none, get_text(type_node));
    return convert_type(mutable_binding, type_node.referenced_type, false); // not optional
}

public convert_parameter_list(emitter: mut Emitter, parameters: Semantic_Node, is_main_function: bool) -> string
{
    assert(parameters.kind == ParameterList, "parameters.kind=" + parameters.kind);
    let builder: mut system.text.String_Builder = new system.text.String_Builder();
    builder.Append("(");
    var first_parameter: bool = true;
    for let parameter: Semantic_Node in children_of_kind(parameters, Parameter)
    {
        if not first_parameter
            { builder.Append(", "); }
        else
            { first_parameter = false; }

        let mutable_binding: bool = node_has_child(parameter, VarKeyword);
        let type_node: Semantic_Node = parameter.children[|parameter.children| - 1];
        builder.Append(convert_type(mutable_binding, type_node));
        builder.Append(" ");
        builder.Append(get_text(first_child(parameter, Identifier)));
        builder.Append("_");

        if is_main_function
        {
            var type_string: string;
            if type_node.kind == MutableType
                { type_string = get_text(type_node.children[0]); }
            else
                { type_string = get_text(type_node); }
            if type_string == "system.console.Console"
                { emitter.main_function_accepts_console = true; }
            if type_string == "system.console.Arguments"
                { emitter.main_function_accepts_args = true; }
        }
    }
    builder.Append(")");
    return builder.ToString();
}

public convert_parameter_list(emitter: mut Emitter, parameters: Semantic_Node) -> string
{
    return convert_parameter_list(emitter, parameters, false);
}

public convert_expression(syntax: Semantic_Node, builder: mut Source_File_Builder) -> void
{
    if syntax.kind == NewExpression
    {
        var constructor_name: string = "";
        var type_node: Semantic_Node = syntax.children[0];
        if type_node.kind == QualifiedName
        {
            let name_node: Semantic_Node = type_node.children[1];

            // We assume that if there is no type, then it is a named constructor
            if name_node.kind == IdentifierName
                and name_node.referenced_type == none
            {
                // Named constructor
                constructor_name = "__" + get_text(name_node);
                type_node = type_node.children[0];
            }
        }

        let type: Type = type_node.referenced_type;
        assert(type =/= none, get_text(type_node));
        write(builder, "new_");
        write(builder, convert_type_name(type).ToString());
        write(builder, constructor_name);

        let argumentList: Semantic_Node = syntax.children[1];
        convert_expression(argumentList, builder);
    }
    else if syntax.kind == ArgumentList
    {
        write(builder, "(");
        var first_expression: bool = true;
        for let arg: Semantic_Node in syntax.children
        {
            if arg.kind =/= LeftParen
                and arg.kind =/= RightParen
                and arg.kind =/= Comma
            {
                if first_expression
                    { first_expression = false; }
                else
                    { write(builder, ", "); }
                convert_expression(arg, builder);
            }
        }
        write(builder, ")");
    }
    else if syntax.kind == NotExpression
    {
        // TODO change to other functions based on argument type
        write(builder, "bit_not(");
        convert_expression(syntax.children[1], builder);
        write(builder, ")");
    }
    else if syntax.kind == MagnitudeExpression
    {
        convert_expression(syntax.children[1], builder);
        convert_member_access(syntax.children[1], builder);
        write(builder, "op_magnitude()");
    }
    else if syntax.kind == ParenthesizedExpression
    {
        convert_expression(syntax.children[1], builder);
    }
    else if syntax.kind == NegateExpression
    {
        write(builder, "i32_negate(");
        convert_expression(syntax.children[1], builder);
        write(builder, ")");
    }
    else if syntax.kind == NoneLiteralExpression
    {
        write(builder, "none");
    }
    else if syntax.kind == SelfExpression
    {
        write(builder, "self");
    }
    else if syntax.kind == NumericLiteralExpression
    {
        write(builder, "i32(" + get_text(syntax) + ")");
    }
    else if syntax.kind == TrueLiteralExpression
        or syntax.kind == FalseLiteralExpression
    {
        write(builder, "bit_" + get_text(syntax));
    }
    else if syntax.kind == CodePointLiteralExpression
    {
        write(builder, "cp(" + get_text(syntax) + ")");
    }
    else if syntax.kind == IdentifierName
    {
        write(builder, get_text(syntax) + "_");
    }
    else if syntax.kind == StringLiteralExpression
    {
        write(builder, "str(" + get_text(syntax) + ")");
    }
    else if syntax.kind == AssignmentExpression
    {
        let operator: int = syntax.children[1].kind;
        if operator == Equals
        {
            convert_expression(syntax.children[0], builder);
            write(builder, " ");
            write(builder, get_text(syntax.children[1]));
            write(builder, " ");
            convert_expression(syntax.children[2], builder);
        }
        else
        {
            convert_expression(syntax.children[0], builder);
            convert_member_access(syntax.children[0], builder);
            write(builder, "op_");
            if operator == PlusEquals
            {
                write(builder, "add_assign");
            }
            else if operator == MinusEquals
            {
                write(builder, "subtract_assign");
            }
            else
            {
                error(builder, "Unsupported assignment operator " + operator);
            }
            write(builder, "(");
            convert_expression(syntax.children[2], builder);
            write(builder, ")");
        }
    }
    else if syntax.kind == EqualExpression
    {
        write(builder, "equal_op(");
        convert_expression(syntax.children[0], builder);
        write(builder, ", ");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else if syntax.kind == NotEqualExpression
    {
        write(builder, "not_equal_op(");
        convert_expression(syntax.children[0], builder);
        write(builder, ", ");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else if syntax.kind == ComparisonExpression
    {
        // TODO assert the left and right types match
        let operator: int = syntax.children[1].kind;
        let type: Type = syntax.children[0].of_type;
        if type =/= none
        {
            write(builder, convert_primitive_type_name(type));
            if operator == LessThan
            {
                write(builder, "_less_than");
            }
            else if operator == LessThanEquals
            {
                write(builder, "_less_than_or_equal");
            }
            else if operator == GreaterThan
            {
                write(builder, "_greater_than");
            }
            else if operator == GreaterThanEquals
            {
                write(builder, "_greater_than_or_equal");
            }
            else
            {
                error(builder, "Unsupported comparison operator " + operator);
            }
            write(builder, "(");
            convert_expression(syntax.children[0], builder);
            write(builder, ", ");
            convert_expression(syntax.children[2], builder);
            write(builder, ")");
        }
        else
        {
            // Old style comparison, method on lhs
            convert_expression(syntax.children[0], builder);
            convert_member_access(syntax.children[0], builder);
            write(builder, "op_");
            if operator == LessThan
            {
                write(builder, "less_than");
            }
            else if operator == LessThanEquals
            {
                write(builder, "less_than_or_equal");
            }
            else if operator == GreaterThan
            {
                write(builder, "greater_than");
            }
            else if operator == GreaterThanEquals
            {
                write(builder, "greater_than_or_equal");
            }
            else
            {
                error(builder, "Unsupported comparison operator " + operator);
            }
            write(builder, "(");
            convert_expression(syntax.children[2], builder);
            write(builder, ")");
        }
    }
    else if syntax.kind == AddExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_member_access(syntax.children[0], builder);
        write(builder, "op_add(");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else if syntax.kind == SubtractExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_member_access(syntax.children[0], builder);
        write(builder, "op_subtract(");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else if syntax.kind == MultiplyExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_member_access(syntax.children[0], builder);
        write(builder, "op_multiply(");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else if syntax.kind == DivideExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_member_access(syntax.children[0], builder);
        write(builder, "op_divide(");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else if syntax.kind == RemainderExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_member_access(syntax.children[0], builder);
        write(builder, "op_remainder(");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else if syntax.kind == OrExpression
    {
        // TODO Based on the type of the values involved, either:
        // * bit_op(bit_arg(lhs) || bit_arg(rhs))
        // * ({ T _lhs = lhs; op_true(_lhs) ? _lhs : op_or(_lhs, rhs); })
        // Note: that is a "compound expression"
        write(builder, "bit_op(bit_arg(");
        convert_expression(syntax.children[0], builder);
        write(builder, ") || bit_arg(");
        convert_expression(syntax.children[2], builder);
        write(builder, "))");
    }
    else if syntax.kind == AndExpression
    {
        // TODO Based on the type of the values involved, either:
        // * bit_op(bit_arg(lhs) && bit_arg(rhs))
        // * ({ T _lhs = lhs; op_false(_lhs) ? _lhs : op_and(_lhs, rhs); })
        // Note: that is a "compound expression"
        write(builder, "bit_op(bit_arg(");
        convert_expression(syntax.children[0], builder);
        write(builder, ") && bit_arg(");
        convert_expression(syntax.children[2], builder);
        write(builder, "))");
    }
    else if syntax.kind == InvocationExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_expression(syntax.children[1], builder);
    }
    else if syntax.kind == MemberAccessExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_member_access(syntax.children[0], builder);
        convert_expression(syntax.children[1], builder);
    }
    else if syntax.kind == ElementAccessExpression
    {
        convert_expression(syntax.children[0], builder);
        convert_member_access(syntax.children[0], builder);
        write(builder, "op_Element(");
        convert_expression(syntax.children[2], builder);
        write(builder, ")");
    }
    else
    {
        error(builder, "Could not convert expression of type " + syntax.kind);
    }
}

public convert_member_access(lhs: Semantic_Node, builder: mut Source_File_Builder) -> void
{
    let type: Type? = lhs.of_type;
    if type =/= none and type.is_value_type // TODO eventually, we shouldn't need to check if the type is null
        and not (type.is_primitive // optional reference types are still pointers
                and unqualified_name(type.name) == "optional"
                and not type.type_parameters[0].is_value_type)
        { write(builder, "."); }
    else
        { write(builder, "->"); }
}

public emit_statement(emitter: mut Emitter, statement: Semantic_Node) -> void
{
    statement_separator_line(emitter.definitions);

    if statement.kind == ReturnStatement
    {
        if |statement.children| == 2
            { write_line(emitter.definitions, "return;"); }
        else
        {
            begin_line(emitter.definitions, "return ");
            convert_expression(statement.children[1], emitter.definitions);
            end_line(emitter.definitions, ";");
        }
    }
    else if statement.kind == LoopStatement
    {
        write_line(emitter.definitions, "for (;;)");
        emit_statement(emitter, statement.children[1]); // Block Statement
    }
    else if statement.kind == Block
    {
        begin_block(emitter.definitions);
        for let block_statement: Semantic_Node in node_statements(statement)
        {
            emit_statement(emitter, block_statement);
        }
        end_block(emitter.definitions);
    }
    else if statement.kind == WhileStatement
    {
        begin_line(emitter.definitions, "while (cond(");
        convert_expression(statement.children[1], emitter.definitions);
        end_line(emitter.definitions, "))");
        emit_statement(emitter, statement.children[2]); // Block Statement
    }
    else if statement.kind == ForStatement
    {
        begin_line(emitter.definitions, "for (");
        let variable_declaration: Semantic_Node = first_child(statement, VariableDeclaration);
        let mutable_binding: bool = node_has_child(variable_declaration, VarKeyword);
        let name: string = get_text(first_child(variable_declaration, Identifier));
        let type: Semantic_Node = variable_declaration.children[2];
        write(emitter.definitions, convert_type(mutable_binding, type) + " " + name + "_");
        write(emitter.definitions, " : *(");
        convert_expression(statement.children[3], emitter.definitions); // expression
        end_line(emitter.definitions, "))");
        emit_statement(emitter, statement.children[4]); // Block Statement
    }
    else if statement.kind == DoWhileStatement
    {
        write_line(emitter.definitions, "do");
        emit_statement(emitter, statement.children[1]); // Block Statement
        begin_line(emitter.definitions, "while (cond(");
        convert_expression(statement.children[3], emitter.definitions);
        end_line(emitter.definitions, "));");
    }
    else if statement.kind == IfStatement
    {
        var if_statement: Semantic_Node? = statement;
        begin_line(emitter.definitions, "");
        loop
        {
            write(emitter.definitions, "if (cond(");
            convert_expression(if_statement.children[1], emitter.definitions);
            end_line(emitter.definitions, "))");
            emit_statement(emitter, if_statement.children[2]); // Block Statement
            let elseClause: Semantic_Node? = first_child(if_statement, ElseClause);
            if elseClause =/= none
            {
                if_statement = first_child(elseClause, IfStatement);
                if if_statement =/= none
                {
                    begin_line(emitter.definitions, "else ");
                }
                else
                {
                    write_line(emitter.definitions, "else");
                    emit_statement(emitter, elseClause.children[1]); // Block Statement
                    break;
                }
            }
            else
                { break; }
        }
    }
    else if statement.kind == BreakStatement
    {
        write_line(emitter.definitions, "break;");
    }
    else if statement.kind == ContinueStatement
    {
        write_line(emitter.definitions, "continue;");
    }
    else if statement.kind == LocalDeclarationStatement
    {
        let variable_declaration: Semantic_Node = first_child(statement, VariableDeclaration);
        let variable_name: string = get_text(first_child(variable_declaration, Identifier));
        let variable_type: Semantic_Node = variable_declaration.children[2];
        let mutable_binding: bool = node_has_child(variable_declaration, VarKeyword);
        begin_line(emitter.definitions, convert_type(mutable_binding, variable_type));
        write(emitter.definitions, " " + variable_name + "_");
        if |variable_declaration.children| > 3
        {
            write(emitter.definitions, " = ");
            convert_expression(variable_declaration.children[3], emitter.definitions);
        }
        end_line(emitter.definitions, ";");
    }
    else if statement.kind == ExpressionStatement
    {
        begin_line(emitter.definitions, "");
        convert_expression(statement.children[0], emitter.definitions);
        end_line(emitter.definitions, ";");
    }
    else
    {
        NOT_IMPLEMENTED("statement.kind=" + statement.kind);
    }
}

public emit_method_body(emitter: mut Emitter, block: Semantic_Node, is_associated_function: bool) -> void
{
    begin_block(emitter.definitions);
    if not is_associated_function
    {
        write_line(emitter.definitions, "auto self = this;");
    }
    for let statement: Semantic_Node in node_statements(block)
    {
        emit_statement(emitter, statement);
    }
    end_block(emitter.definitions);
}

public emit_constructor_body(emitter: mut Emitter, block: Semantic_Node, self_type: string, is_value_type: bool) -> void
{
    // TODO this doesn't support explicit return from constructors
    begin_block(emitter.definitions);
    if is_value_type
    {
        write_line(emitter.definitions, self_type + " self;");
    }
    else
    {
        write_line(emitter.definitions, self_type + " self = this;");
    }
    for let statement: Semantic_Node in node_statements(block)
    {
        emit_statement(emitter, statement);
    }
    write_line(emitter.definitions, "return self;");
    end_block(emitter.definitions);
}

public emit_access_modifer(emitter: mut Emitter, current_access_level: int, access_modifer: int) -> int
{
    if access_modifer =/= current_access_level
    {
        // We avoid the indent by using end_line()
        if access_modifer == PublicKeyword
            or access_modifer == InternalKeyword
        {
            end_line(emitter.class_declarations, "public:");
            return PublicKeyword;
        }
        else if access_modifer == ProtectedKeyword
        {
            end_line(emitter.class_declarations, "public:");
        }
        else if access_modifer == PrivateKeyword
        {
            end_line(emitter.class_declarations, "private:");
        }
        else
        {
            THROW_EXCEPTION("Invalid access modifier in Emitter.emit_member_declaration(). kind is " + access_modifer);
        }
    }
    return access_modifer;
}

public emit_member_declaration(emitter: mut Emitter, member: Semantic_Node, class_name: string, is_value_type: bool, current_access_level: int) -> int
{
    var access_modifer: int = node_access_modifier(member).kind;
    if member.kind == ConstructorDeclaration // always make constructors public so they can be called by `new_` functions
        { access_modifer = PublicKeyword; }
    access_modifer = emit_access_modifer(emitter, current_access_level, access_modifer);
    if member.kind == ConstructorDeclaration
    {
        let parameters: string = convert_parameter_list(emitter, first_child(member, ParameterList));
        var new_function_name: string = "new_" + class_name;
        var constructor_full_name: string = "construct";
        let constructor_name_node: Semantic_Node? = first_child(member, Identifier);
        if constructor_name_node =/= none
        {
            let constructor_name: string = get_text(constructor_name_node);
            new_function_name = new_function_name + "__" + constructor_name;
            constructor_full_name = "construct_" + constructor_name;
        }
        var return_type: string = class_name;
        if not is_value_type
            { return_type = return_type + " *_Nonnull"; }
        // Write out the declaration for C++ so we can call functions defined after others
        begin_line(emitter.class_declarations, "");
        if is_value_type { write(emitter.class_declarations, "static "); }
        end_line(emitter.class_declarations, "auto " + constructor_full_name + parameters + " -> " + return_type + ";");
        element_separator_line(emitter.definitions);
        write_line(emitter.definitions, "auto " + class_name + "::" + constructor_full_name + parameters + " -> " + return_type);
        emit_constructor_body(emitter, first_child(member, Block), return_type, is_value_type);
        // Write out new function
        write_line(emitter.function_declarations, "inline " + return_type + " " + new_function_name + parameters + ";");
        element_separator_line(emitter.definitions);
        write_line(emitter.definitions, "inline " + return_type + " " + new_function_name + parameters);
        begin_block(emitter.definitions);
        if not is_value_type
            { begin_line(emitter.definitions, "return (new " + class_name + "())->" + constructor_full_name + "("); }
        else
            { begin_line(emitter.definitions, "return " + class_name + "::" + constructor_full_name + "("); }
        var first_parameter: bool = true;
        for let parameter: Semantic_Node in first_child(member, ParameterList).children
        {
            if not first_parameter
                { write(emitter.definitions, ", "); }
            else
                { first_parameter = false; }

            write(emitter.definitions, get_text(first_child(parameter, Identifier)) + "_");
        }
        end_line(emitter.definitions, ");");
        end_block(emitter.definitions);
    }
    else if member.kind == FieldDeclaration
    {
        let variable_declaration: Semantic_Node = first_child(member, VariableDeclaration);
        let field_name: string = get_text(first_child(variable_declaration, Identifier));
        let field_type: Semantic_Node = variable_declaration.children[2];
        let cpp_type: string = convert_type(true, field_type); // because we need to assign them in constructors don't mark fields const
        write_line(emitter.class_declarations, cpp_type + " " + field_name + "_;");
    }
    else if member.kind == MethodDeclaration
    {
        let method_name: string = get_text(first_child(member, Identifier));
        let parameters: string = convert_parameter_list(emitter, first_child(member, ParameterList));
        let self_parameter: Semantic_Node? = first_child(first_child(member, ParameterList), SelfParameter);
        let is_associated_function: bool = self_parameter == none;
        let mutable_self: bool = not is_associated_function and node_has_child(self_parameter, MutableKeyword);
        let return_type_node: Semantic_Node = member.children[3];
        let cpp_type: string = convert_type(true, return_type_node);
        var static_modifier: string = "";
        if is_associated_function
            { static_modifier = "static "; }
        var constModifier: string = "";
        if not mutable_self and not is_associated_function
            { constModifier = "const "; }
        write_line(emitter.class_declarations, static_modifier + "auto " + method_name + "_" + parameters + " " + constModifier + "-> " + cpp_type + ";");
        element_separator_line(emitter.definitions);
        write_line(emitter.definitions, "auto ::" + class_name + "::" + method_name + "_" + parameters + " " + constModifier + "-> " + cpp_type);
        let block: Semantic_Node = first_child(member, Block);
        emit_method_body(emitter, block, is_associated_function);
    }
    else
    {
        error(emitter.definitions, "Could not emit member of type " + member.kind);
    }

    return access_modifer;
}

public emit_default_constructor(emitter: mut Emitter, type_name: string, is_value_type: bool) -> void
{
    let default_constructor_name: string = "c_"+type_name;

    var return_type: string = type_name;
    if not is_value_type
        { return_type = return_type + " *_Nonnull"; }

    var constructor_signature: string = return_type + " " + default_constructor_name + "(";
    if not is_value_type
        { constructor_signature = constructor_signature + return_type + " self"; }
    constructor_signature = constructor_signature + ")";

    // Constructor Declaration
    write_line(emitter.function_declarations, constructor_signature + ";");

     // Constructor definition
    element_separator_line(emitter.definitions);
    begin_line(emitter.definitions, constructor_signature);
    // TODO I probably need to do the default initalization
    if is_value_type
        { end_line(emitter.definitions, " { return {}; }"); }
    else
        { end_line(emitter.definitions, " { return self; }"); }

    // `new_` function declaration
    let new_function_signature: string = "inline " + return_type + " new_" + type_name + "()";
    write_line(emitter.function_declarations, new_function_signature + ";");

    // `new_` function definition
    element_separator_line(emitter.definitions);
    write_line(emitter.definitions, new_function_signature);
    begin_block(emitter.definitions);
    if not is_value_type
        { write_line(emitter.definitions, "return "+default_constructor_name+"(new " + type_name + "());"); }
    else
        { write_line(emitter.definitions, "return " + default_constructor_name + "();"); }
    end_block(emitter.definitions);
}

public emit_declaration(emitter: mut Emitter, declaration: Semantic_Node) -> void
{
    if declaration.kind == GlobalDeclaration
    {
        let variable_declaration: Semantic_Node = first_child(declaration, VariableDeclaration);
        let variable_name: string = get_text(first_child(variable_declaration, Identifier));
        let variable_type: Semantic_Node = variable_declaration.children[2];
        let mutable_binding: bool = node_has_child(variable_declaration, VarKeyword);
        let cpp_type: string = convert_type(mutable_binding, variable_type);
        begin_line(emitter.global_definitions, cpp_type);
        write(emitter.global_definitions, " " + variable_name + "_ = ");
        let expression: Semantic_Node = variable_declaration.children[3];
        convert_expression(expression, emitter.global_definitions);
        end_line(emitter.global_definitions, ";");
    }
    else if declaration.kind == ClassDeclaration
    {
        let declared_type: Type = declaration.declares_type;
        assert(declared_type =/= none, get_text(first_child(declaration, Identifier)));
        let class_name: string = mangle_name(declared_type.name);
        write_line(emitter.type_declarations, "class " + class_name + ";");
        element_separator_line(emitter.class_declarations);
        write_line(emitter.class_declarations, "class " + class_name);
        begin_block(emitter.class_declarations);
        end_line(emitter.class_declarations, "public:"); // Assume we aren't nested, avoid the indent
        var current_access_level: int = PublicKeyword;
        var has_constructors: bool = false;
        for let member: Semantic_Node in node_members(declaration)
        {
            has_constructors = has_constructors or member.kind == ConstructorDeclaration;
            current_access_level = emit_member_declaration(emitter, member, class_name, false, current_access_level);
        }
        if not has_constructors
        {
            emit_default_constructor(emitter, class_name, false
            );
        }
        end_block_with_semicolon(emitter.class_declarations);
    }
    else if declaration.kind == StructDeclaration
    {
        let declared_type: Type = declaration.declares_type;
        assert(declared_type =/= none, get_text(first_child(declaration, Identifier)));
        let struct_name: string = mangle_name(declared_type.name);
        write_line(emitter.type_declarations, "struct " + struct_name + ";");
        element_separator_line(emitter.class_declarations);
        write_line(emitter.class_declarations, "struct " + struct_name + " final");
        begin_block(emitter.class_declarations);
        end_line(emitter.class_declarations, "public:"); // Assume we aren't nested, avoid the indent
        // Write out arrow and dereference operators for using value types like reference types
        // TODO Emit correct code to use value types as value types rather than reference types and drop this code
        write_line(emitter.class_declarations, struct_name + " * operator->() { return this; }");
        write_line(emitter.class_declarations, struct_name + " const * operator->() const { return this; }");
        write_line(emitter.class_declarations, struct_name + " & operator* () { return *this; }");
        write_line(emitter.class_declarations, struct_name + " const & operator* () const { return *this; }");
        var current_access_level: int = PublicKeyword;
        var has_constructors: bool = false;
        for let member: Semantic_Node in node_members(declaration)
        {
            has_constructors = has_constructors or member.kind == ConstructorDeclaration;
            current_access_level = emit_member_declaration(emitter, member, struct_name, true, current_access_level);
        }
        if not has_constructors
        {
            emit_default_constructor(emitter, struct_name, true);
        }
        end_block_with_semicolon(emitter.class_declarations);
    }
    else if declaration.kind == EnumDeclaration
    {
        let enum_name: string = get_text(first_child(declaration, Identifier));
        write_line(emitter.type_declarations, "enum class " + enum_name + "_;");
        element_separator_line(emitter.class_declarations);
        write_line(emitter.class_declarations, "enum class " + enum_name + "_");
        begin_block(emitter.class_declarations);
        for let member: Semantic_Node in declaration.children
        {
            if member.kind == EnumMemberDeclaration
            {
                let member_name: string = get_text(member.children[0]);
                begin_line(emitter.class_declarations, member_name + "_");
                let member_value: Semantic_Node? = first_child(member, Number);
                if member_value =/= none
                {
                    write(emitter.class_declarations, " = ");
                    write(emitter.class_declarations, get_text(member_value));
                }
                end_line(emitter.class_declarations, ",");
            }
        }
        end_block_with_semicolon(emitter.class_declarations);
    }
    else if declaration.kind == FunctionDeclaration
    {
        let name: string = get_text(first_child(declaration, Identifier));
        let is_main: bool = name == "main";
        if is_main { emitter.main_function_name = name; }
        let parameters: string = convert_parameter_list(emitter, first_child(declaration, ParameterList), is_main);
        let return_type_node: Semantic_Node = declaration.children[3];
        let cpp_type: string = convert_type(true, return_type_node);
        // Write out the function declaration for C++ so we can call functions defined after others
        write_line(emitter.function_declarations, "auto " + name + "_" + parameters + " -> " + cpp_type + ";");
        element_separator_line(emitter.definitions);
        write_line(emitter.definitions, "auto " + name + "_" + parameters + " -> " + cpp_type);
        if is_main
        {
            if emitter.main_function_return_type =/= ""
                { error(emitter.definitions, "Multiple declarations of main"); }
            emitter.main_function_return_type = cpp_type;
        }
        emit_statement(emitter, first_child(declaration, Block));
    }
    else if declaration.kind == EndOfFileToken
    {
        // Ignore the end of file
    }
    else
    {
        error(emitter.definitions, "Could not emit declaration of type " + declaration.kind);
    }
}

public emit_compilation_unit(emitter: mut Emitter, unit: Compilation_Unit) -> void
{
    for let declaration: Semantic_Node in unit.declarations
    {
        emit_declaration(emitter, declaration);
    }
}

public emit_preamble(emitter: mut Emitter) -> void
{
    // Setup the beginning of each section
    write_line(emitter.type_declarations, "#include \"RuntimeLibrary.hpp\"");
    blank_line(emitter.type_declarations);
    write_line(emitter.type_declarations, "// Type Declarations");
    blank_line(emitter.function_declarations);
    write_line(emitter.function_declarations, "// Function Declarations");
    blank_line(emitter.class_declarations);
    write_line(emitter.class_declarations, "// Class Declarations");
    blank_line(emitter.global_definitions);
    write_line(emitter.global_definitions, "// Global Definitions");
    blank_line(emitter.definitions);
    write_line(emitter.definitions, "// Definitions");
}

public emit_entry_point_adapter(emitter: mut Emitter) -> void
{
    element_separator_line(emitter.definitions);
    write_line(emitter.definitions, "// Entry Point Adapter");
    write_line(emitter.definitions, "std::int32_t main(int argc, char const *const * argv)");
    begin_block(emitter.definitions);

    write_line(emitter.definitions, "try");
    begin_block(emitter.definitions);

    for let resource: Source_Text in emitter.resources
    {
        begin_line(emitter.definitions, "resource_manager_->AddResource(str(\"");
        write(emitter.definitions, resource.name);
        write(emitter.definitions, "\"), str(\"");
        write(emitter.definitions, resource.text
                                .Replace("\\", "\\\\")
                                .Replace("\n", "\\n")
                                .Replace("\r", "\\r")
                                .Replace("\"", "\\\""));
        end_line(emitter.definitions, "\"));");
    }
    if |emitter.resources| > 0
    {
        end_line(emitter.definitions, ""); // Use EndLine to avoid having an indent on a blank line
    }

    // TODO should be `new mut`
    let args: mut system.text.String_Builder = new system.text.String_Builder();
    if emitter.main_function_accepts_console
        { args.Append("new t_system__console__Console()"); }
    if emitter.main_function_accepts_args
    {
        if emitter.main_function_accepts_console
            { args.Append(", "); }
        args.Append("new t_system__console__Arguments(argc, argv)");
    }

    if emitter.main_function_return_type == "void"
    {
        write_line(emitter.definitions, emitter.main_function_name + "_(" + args.ToString() + ");");
        write_line(emitter.definitions, "return 0;");
    }
    else
    {
        write_line(emitter.definitions, "return " + emitter.main_function_name + "_(" + args.ToString() + ").value;");
    }

    end_block(emitter.definitions); // try
    write_line(emitter.definitions, "catch(std::exception &ex)");
    begin_block(emitter.definitions);
    write_line(emitter.definitions, "std::printf(\"Program exited due to error:\\n\");");
    write_line(emitter.definitions, "std::printf(\"%s\", ex.what());");
    write_line(emitter.definitions, "return 70;"); // Defined by sysexits.h as EX_SOFTWARE
    end_block(emitter.definitions);

    end_block(emitter.definitions);
}
