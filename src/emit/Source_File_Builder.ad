public class Source_File_Builder
{
    public let code: mut system.text.String_Builder;
    public let indent: mut system.text.String_Builder; // TODO expose length as IndentDepth
    public var firstElement: bool; // TODO expose public getter
    public var afterBlock: bool; // TODO expose public getter

    public new()
    {
        self.code = new system.text.String_Builder();
        self.indent = new system.text.String_Builder();
        self.firstElement = true;
        self.afterBlock = true;
    }
}

public error(file: mut Source_File_Builder, message: string) -> void
{
    sb_append(file.code, format_error(message));
}

public begin_line(file: mut Source_File_Builder, value: string) -> void
{
    sb_append_sb(file.code, file.indent);
    sb_append(file.code, value);
    file.firstElement = file.afterBlock = false;
}

public write(file: mut Source_File_Builder, value: string) -> void
{
    sb_append(file.code, value);
    file.firstElement = file.afterBlock = false;
}

public end_line(file: mut Source_File_Builder, value: string) -> void
{
    sb_append(file.code, value);
    sb_append_line(file.code); // TODO need to think about the handling of newlines
    file.firstElement = file.afterBlock = false;
}

public write_line(file: mut Source_File_Builder, value: string) -> void
{
    sb_append_sb(file.code, file.indent);
    sb_append(file.code, value);
    sb_append_line(file.code);
    file.firstElement = file.afterBlock = false;
}

public blank_line(file: mut Source_File_Builder) -> void
{
    sb_append_line(file.code);
    file.firstElement = true;
    file.afterBlock = false;
}

public element_separator_line(file: mut Source_File_Builder) -> void
{
    if not file.firstElement
    {
        sb_append_line(file.code);
        file.firstElement = true;
        file.afterBlock = false;
    }
}

public statement_separator_line(file: mut Source_File_Builder) -> void
{
    if file.afterBlock
    {
        sb_append_line(file.code);
        file.firstElement = true;
        file.afterBlock = false;
    }
}

// @ensures IndentDepth == old(IndentDepth)+1
public begin_block(file: mut Source_File_Builder) -> void
{
    write_line(file, "{");
    sb_append(file.indent, "\t");
    file.firstElement = file.afterBlock = false;
}

// @requires IndentDepth > 0
// @ensures IndentDepth == old(IndentDepth)-1
public end_block(file: mut Source_File_Builder) -> void
{
    sb_remove(file.indent, 0, 1);
    write_line(file, "}");
    file.afterBlock = true;
}

// @requires IndentDepth > 0
// @ensures IndentDepth == old(IndentDepth)-1
public end_block_with_semicolon(file: mut Source_File_Builder) -> void
{
    sb_remove(file.indent, 0, 1);
    write_line(file, "};");
}

public byte_length(file: Source_File_Builder) -> int
{
    return file.code.byte_length;
}

public to_string(file: mut Source_File_Builder) -> string
{
    return sb_to_string(file.code);
}
