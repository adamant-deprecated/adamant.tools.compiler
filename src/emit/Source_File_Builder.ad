public class Source_File_Builder
{
    public let code: mut system.text.String_Builder;
    public let indent: mut system.text.String_Builder; // TODO expose length as IndentDepth
    public var firstElement: bool; // TODO expose public getter
    public var afterBlock: bool; // TODO expose public getter

    public new()
    {
        code = new system.text.String_Builder();
        indent = new system.text.String_Builder();
        firstElement = true;
        afterBlock = true;
    }
}

public error(file: mut Source_File_Builder, message: string) -> void
{
    file.code.Append(format_error(message));
}

public begin_line(file: mut Source_File_Builder, value: string) -> void
{
    file.code.Append(file.indent);
    file.code.Append(value);
    file.firstElement = file.afterBlock = false;
}

public write(file: mut Source_File_Builder, value: string) -> void
{
    file.code.Append(value);
    file.firstElement = file.afterBlock = false;
}

public end_line(file: mut Source_File_Builder, value: string) -> void
{
    file.code.Append(value);
    file.code.AppendLine(); // TODO need to think about the handling of newlines
    file.firstElement = file.afterBlock = false;
}

public write_line(file: mut Source_File_Builder, value: string) -> void
{
    file.code.Append(file.indent);
    file.code.Append(value);
    file.code.AppendLine();
    file.firstElement = file.afterBlock = false;
}

public blank_line(file: mut Source_File_Builder) -> void
{
    file.code.AppendLine();
    file.firstElement = true;
    file.afterBlock = false;
}

public element_separator_line(file: mut Source_File_Builder) -> void
{
    if not file.firstElement
    {
        file.code.AppendLine();
        file.firstElement = true;
        file.afterBlock = false;
    }
}

public statement_separator_line(file: mut Source_File_Builder) -> void
{
    if file.afterBlock
    {
        file.code.AppendLine();
        file.firstElement = true;
        file.afterBlock = false;
    }
}

// @ensures IndentDepth == old(IndentDepth)+1
public begin_block(file: mut Source_File_Builder) -> void
{
    write_line(file, "{");
    file.indent.Append("\t");
    file.firstElement = file.afterBlock = false;
}

// @requires IndentDepth > 0
// @ensures IndentDepth == old(IndentDepth)-1
public end_block(file: mut Source_File_Builder) -> void
{
    file.indent.Remove(0, 1);
    write_line(file, "}");
    file.afterBlock = true;
}

// @requires IndentDepth > 0
// @ensures IndentDepth == old(IndentDepth)-1
public end_block_with_semicolon(file: mut Source_File_Builder) -> void
{
    file.indent.Remove(0, 1);
    write_line(file, "};");
}

public byte_length(file: Source_File_Builder) -> int
{
    return file.code.byte_length();
}

public to_string(file: mut Source_File_Builder) -> string
{
    return file.code.ToString();
}
