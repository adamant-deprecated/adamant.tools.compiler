public class Compilation_Unit_Parser
{
    public let token_stream: mut Token_Stream;
    public var token: Syntax_Node?;
    public var compilation_unit: Syntax_Node?;

    public new(token_stream: mut Token_Stream)
    {
        self.token_stream = token_stream;
        self.compilation_unit = none; // TODO We currently don't auto-initialize members
    }
}

public parse(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    if parser.compilation_unit == none
    {
        parser.token = next_token(parser.token_stream);
        parser.compilation_unit = parse_compilation_unit(parser);
    }
    return parser.compilation_unit;
}

// ----------------------------------------------------------------------------
// token Handling
// ----------------------------------------------------------------------------

public accept_token(parser: mut Compilation_Unit_Parser) -> Syntax_Node?
{
    let node: Syntax_Node? = parser.token;
    parser.token = next_token(parser.token_stream);
    return node;
}

public expect_token(parser: mut Compilation_Unit_Parser, tokenType: int) -> Syntax_Node
{
    if parser.token == none
    {
        return new Syntax_Node.missing(tokenType, parser.token_stream.source, source_byte_length(parser.token_stream.source));
    }

    if parser.token == none or parser.token.kind =/= tokenType
    {
        return new Syntax_Node.missing(tokenType, parser.token_stream.source, parser.token.start);
    }

    let node: Syntax_Node? = parser.token;
    parser.token = next_token(parser.token_stream);
    return node;
}

// ----------------------------------------------------------------------------
// Parser
// ----------------------------------------------------------------------------

public parse_type_name(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    if parser.token.kind == CodePoint
        or parser.token.kind == String
        or parser.token.kind == Int
        or parser.token.kind == Bool
        or parser.token.kind == Void
        or parser.token.kind == UnsignedInt
    {
        return new Syntax_Node(PredefinedType, accept_token(parser));
    }
    else
    {
        var type: Syntax_Node = new Syntax_Node(IdentifierName, expect_token(parser, Identifier));

        while parser.token.kind == Dot
        {
            let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
            add_item(children, type);
            add_item(children, expect_token(parser, Dot));
            let identifier: Syntax_Node = expect_token(parser, Identifier);
            if parser.token.kind == LessThan
            {
                // Generic Name
                let genericNameChildren: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
                add_item(genericNameChildren, new Syntax_Node(IdentifierName, identifier));
                add_item(genericNameChildren, expect_token(parser, LessThan));
                add_item(genericNameChildren, parse_type(parser));
                // TODO support multiple type arguments
                add_item(genericNameChildren, expect_token(parser, GreaterThan));
                add_item(children, new Syntax_Node(GenericName, genericNameChildren));
            }
            else
            {
                add_item(children, new Syntax_Node(IdentifierName, identifier));
            }

            type = new Syntax_Node(QualifiedName, children);
        }

        return type;
    }
}

public parse_non_optional_type(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();

    if parser.token.kind == MutableKeyword
    {
        add_item(children, expect_token(parser, MutableKeyword));
        add_item(children, parse_type_name(parser));
        return new Syntax_Node(MutableType, children);
    }
    else
    {
        add_item(children, parse_type_name(parser));
        return new Syntax_Node(ImmutableType, children);
    }
}

public parse_type(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    var type: Syntax_Node = parse_non_optional_type(parser);

    while parser.token.kind == Question
    {
        let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
        add_item(children, type);
        add_item(children, expect_token(parser, Question));
        type = new Syntax_Node(ImmutableType, new Syntax_Node(OptionalType, children));
    }

    return type;
}

// An Atom is the unit of an expression that occurs between infix operators, i.e. an identifier, literal, group, or new
public parse_atom(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();

    // This should be a match on the token type
    if parser.token.kind == NewKeyword
    {
        // Constructor Call
        add_item(children, expect_token(parser, NewKeyword));
        add_item(children, parse_type_name(parser));
        add_item(children, parse_call_arguments(parser));
        return new Syntax_Node(NewExpression, children);
    }
    if parser.token.kind == NotOperator
    {
        add_item(children, expect_token(parser, NotOperator));
        add_item(children, parse_expression(parser, 8));
        return new Syntax_Node(NotExpression, children);
    }
    if parser.token.kind == LeftParen
    {
        add_item(children, expect_token(parser, LeftParen));
        add_item(children, parse_expression(parser));
        add_item(children, expect_token(parser, RightParen));
        return new Syntax_Node(ParenthesizedExpression, children);
    }
    if parser.token.kind == Pipe
    {
        add_item(children, expect_token(parser, Pipe));
        add_item(children, parse_expression(parser));
        add_item(children, expect_token(parser, Pipe));
        return new Syntax_Node(MagnitudeExpression, children);
    }
    if parser.token.kind == Minus
    {
        // Unary Minus
        add_item(children, expect_token(parser, Minus));
        add_item(children, parse_expression(parser, 8));
        return new Syntax_Node(NegateExpression, children);
    }
    if parser.token.kind == NoneKeyword
    {
        add_item(children, expect_token(parser, NoneKeyword));
        return new Syntax_Node(NoneLiteralExpression, children);
    }
    if parser.token.kind == SelfKeyword
    {
        add_item(children, expect_token(parser, SelfKeyword));
        return new Syntax_Node(SelfExpression, children);
    }
    if parser.token.kind == TrueKeyword
    {
        add_item(children, expect_token(parser, TrueKeyword));
        return new Syntax_Node(TrueLiteralExpression, children);
    }
    if parser.token.kind == FalseKeyword
    {
        add_item(children, expect_token(parser, FalseKeyword));
        return new Syntax_Node(FalseLiteralExpression, children);
    }
    if parser.token.kind == Number
    {
        add_item(children, expect_token(parser, Number));
        return new Syntax_Node(NumericLiteralExpression, children);
    }
    if parser.token.kind == Identifier
    {
        add_item(children, expect_token(parser, Identifier));
        return new Syntax_Node(IdentifierName, children);
    }
    if parser.token.kind == StringLiteral
    {
        add_item(children, expect_token(parser, StringLiteral));
        return new Syntax_Node(StringLiteralExpression, children);
    }
    if parser.token.kind == CodePointLiteral
    {
        add_item(children, expect_token(parser, CodePointLiteral));
        return new Syntax_Node(CodePointLiteralExpression, children);
    }

    // Missing an atom, assume it should have been an identifier
    return new Syntax_Node(IdentifierName, expect_token(parser, Identifier));
}

public parse_call_arguments(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
    add_item(children, expect_token(parser, LeftParen));
    if parser.token.kind =/= RightParen
    {
        loop
        {
            add_item(children, parse_expression(parser));
            if parser.token.kind == Comma
                { add_item(children, expect_token(parser, Comma)); }
            else
                { break; }
        }
    }
    add_item(children, expect_token(parser, RightParen));
    return new Syntax_Node(ArgumentList, children);
}

// Operator Precedence
// 1 Assignment: = += -=
// 2 Logical Or: or
// 3 Logical And: and
// 4 Equality: == =/=
// 5 Relational: < <= > >=
// 6 Additive: + -
// 7 Multiplicative: * / %
// 8 Unary: - not
// 9 Primary: f() . []
public parse_expression(parser: mut Compilation_Unit_Parser, minPrecedence: int) -> Syntax_Node
{
    var expression: Syntax_Node = parse_atom(parser);

    loop
    {
        let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
        add_item(children, expression);

        var precedence: int;
        var leftAssociative: bool;
        var suffixOperator: bool = false;
        var expressionType: int;
        if (parser.token.kind == Equals or parser.token.kind == PlusEquals or parser.token.kind == MinusEquals) and minPrecedence <= 1
        {
            // Assignment
            precedence = 1;
            leftAssociative = false;
            add_item(children, accept_token(parser));
            expressionType = AssignmentExpression;
        }
        else if parser.token.kind == OrKeyword and minPrecedence <= 2
        {
            // logical or
            precedence = 2;
            leftAssociative = true;
            add_item(children, expect_token(parser, OrKeyword));
            expressionType = OrExpression;
        }
        else if parser.token.kind == AndKeyword and minPrecedence <= 3
        {
            // logical and
            precedence = 3;
            leftAssociative = true;
            add_item(children, expect_token(parser, AndKeyword));
            expressionType = AndExpression;
        }
        else if parser.token.kind == EqualsEquals and minPrecedence <= 4
        {
            // equal
            precedence = 4;
            leftAssociative = true;
            add_item(children, expect_token(parser, EqualsEquals));
            expressionType = EqualExpression;
        }
        else if parser.token.kind == EqualsSlashEquals and minPrecedence <= 4
        {
            // not equal
            precedence = 4;
            leftAssociative = true;
            add_item(children, expect_token(parser, EqualsSlashEquals));
            expressionType = NotEqualExpression;
        }
        else if (parser.token.kind == LessThan or parser.token.kind == LessThanEquals or parser.token.kind == GreaterThan or parser.token.kind == GreaterThanEquals) and minPrecedence <= 5
        {
            // relational operators
            precedence = 5;
            leftAssociative = true;
            add_item(children, accept_token(parser));
            expressionType = ComparisonExpression;
        }
        else if parser.token.kind == Plus and minPrecedence <= 6
        {
            // Additive
            precedence = 6;
            leftAssociative = true;
            add_item(children, expect_token(parser, Plus));
            expressionType = AddExpression;
        }
        else if parser.token.kind == Minus and minPrecedence <= 6
        {
            // Additive
            precedence = 6;
            leftAssociative = true;
            add_item(children, expect_token(parser, Minus));
            expressionType = SubtractExpression;
        }
        else if parser.token.kind == Asterisk and minPrecedence <= 7
        {
            // Multiplicative
            precedence = 7;
            leftAssociative = true;
            add_item(children, expect_token(parser, Asterisk));
            expressionType = MultiplyExpression;
        }
        else if parser.token.kind == Slash and minPrecedence <= 7
        {
            // Multiplicative
            precedence = 7;
            leftAssociative = true;
            add_item(children, expect_token(parser, Slash));
            expressionType = DivideExpression;
        }
        else if parser.token.kind == Percent and minPrecedence <= 7
        {
            // Multiplicative
            precedence = 7;
            leftAssociative = true;
            add_item(children, expect_token(parser, Percent));
            expressionType = RemainderExpression;
        }
        else if parser.token.kind == LeftParen and minPrecedence <= 9
        {
            // Invocation Expression
            precedence = 9;
            leftAssociative = true;
            suffixOperator = true;

            add_item(children, parse_call_arguments(parser));
            expressionType = InvocationExpression;
        }
        else if parser.token.kind == Dot and minPrecedence <= 9
        {
            // Member Access
            precedence = 9;
            leftAssociative = true;
            add_item(children, expect_token(parser, Dot));
            expressionType = MemberAccessExpression;
        }
        else if parser.token.kind == LeftBracket and minPrecedence <= 9
        {
            // Element Access
            precedence = 9;
            leftAssociative = true;
            suffixOperator = true;

            add_item(children, expect_token(parser, LeftBracket));
            add_item(children, parse_expression(parser));
            add_item(children, expect_token(parser, RightBracket));
            expressionType = ElementAccessExpression;
        }
        else
        {
            return expression;
        }

        if not suffixOperator
        {
            if leftAssociative
                { precedence += 1; }
            add_item(children, parse_expression(parser, precedence));
            expression = new Syntax_Node(expressionType, children);
        }
        else
        {
            expression = new Syntax_Node(expressionType, children);
        }
    }
}

public parse_expression(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    return parse_expression(parser, 1);
}

public parse_statement(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();

    if parser.token.kind == ReturnKeyword
    {
        add_item(children, expect_token(parser, ReturnKeyword));
        if parser.token.kind =/= Semicolon
        {
            add_item(children, parse_expression(parser));
        }
        add_item(children, expect_token(parser, Semicolon));
        return new Syntax_Node(ReturnStatement, children);
    }
    if parser.token.kind == LoopKeyword
    {
        add_item(children, expect_token(parser, LoopKeyword));
        add_item(children, parse_block(parser));
        return new Syntax_Node(LoopStatement, children);
    }
    if parser.token.kind == WhileKeyword
    {
        add_item(children, expect_token(parser, WhileKeyword));
        add_item(children, parse_expression(parser));
        add_item(children, parse_block(parser));
        return new Syntax_Node(WhileStatement, children);
    }
    if parser.token.kind == ForKeyword
    {
        add_item(children, expect_token(parser, ForKeyword));
        add_item(children, parse_variable_declaration(parser, false));
        add_item(children, expect_token(parser, InKeyword));
        add_item(children, parse_expression(parser));
        add_item(children, parse_block(parser));
        return new Syntax_Node(ForStatement, children);
    }
    if parser.token.kind == DoKeyword
    {
        add_item(children, expect_token(parser, DoKeyword));
        add_item(children, parse_block(parser));
        add_item(children, expect_token(parser, WhileKeyword));
        add_item(children, parse_expression(parser));
        add_item(children, expect_token(parser, Semicolon));
        return new Syntax_Node(DoWhileStatement, children);
    }
    if parser.token.kind == IfKeyword
    {
        return parse_if_statement(parser);
    }
    if parser.token.kind == BreakKeyword
    {
        add_item(children, expect_token(parser, BreakKeyword));
        add_item(children, expect_token(parser, Semicolon));
        return new Syntax_Node(BreakStatement, children);
    }
    if parser.token.kind == ContinueKeyword
    {
        add_item(children, expect_token(parser, ContinueKeyword));
        add_item(children, expect_token(parser, Semicolon));
        return new Syntax_Node(ContinueStatement, children);
    }
    if parser.token.kind == VarKeyword or parser.token.kind == LetKeyword
    {
        add_item(children, parse_variable_declaration(parser, true));
        add_item(children, expect_token(parser, Semicolon));
        return new Syntax_Node(LocalDeclarationStatement, children);
    }
    if parser.token.kind == LeftBrace
    {
        return parse_block(parser);
    }

    // Assume it is an expression statement
    add_item(children, parse_expression(parser));
    add_item(children, expect_token(parser, Semicolon));
    return new Syntax_Node(ExpressionStatement, children);
}

public parse_if_statement(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();

    add_item(children, expect_token(parser, IfKeyword));
    add_item(children, parse_expression(parser));
    add_item(children, parse_block(parser));
    if parser.token.kind == ElseKeyword
    {
        let elseChildren: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
        add_item(elseChildren, expect_token(parser, ElseKeyword));
        if parser.token.kind == IfKeyword
            { add_item(elseChildren, parse_if_statement(parser)); }
        else
            { add_item(elseChildren, parse_block(parser));}
        add_item(children, new Syntax_Node(ElseClause, elseChildren));
    }

    return new Syntax_Node(IfStatement, children);
}

public parse_variable_declaration(parser: mut Compilation_Unit_Parser, allowInitializer: bool) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();

    if parser.token.kind == LetKeyword and parser.token.kind =/= VarKeyword
        { add_item(children, expect_token(parser, LetKeyword)); }
    else
        { add_item(children, expect_token(parser, VarKeyword)); }

    add_item(children, expect_token(parser, Identifier));
    add_item(children, expect_token(parser, Colon));
    add_item(children, parse_type(parser));

    if allowInitializer and parser.token.kind == Equals
    {
        add_item(children, expect_token(parser, Equals));
        add_item(children, parse_expression(parser));
    }

    return new Syntax_Node(VariableDeclaration, children);
}

public parse_block(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
    add_item(children, expect_token(parser, LeftBrace));
    while parser.token.kind =/= RightBrace and parser.token.kind =/= EndOfFileToken
    {
        let startToken: Syntax_Node = parser.token;
        add_item(children, parse_statement(parser));
        if parser.token == startToken
        {
            // We have not advanced at all when trying to parse a statement.
            // Better skip some tokens to get unstuck
            let skipped: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
            while parser.token.kind =/= LeftBrace
                and parser.token.kind =/= RightBrace
                and parser.token.kind =/= EndOfFileToken
            {
                let currentTokenType: int = parser.token.kind;
                add_item(skipped, accept_token(parser));
                // If we hit a semicolon, assume that was the end of the statement
                if currentTokenType == Semicolon
                    { break; }
            }
            add_item(children, new Syntax_Node.skipped(skipped));
        }
    }
    add_item(children, expect_token(parser, RightBrace));
    return new Syntax_Node(Block, children);
}

public parse_parameter_list(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
    add_item(children, expect_token(parser, LeftParen));

    if parser.token.kind =/= RightParen
    {
        loop
        {
            let parameterChildren: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
            if parser.token.kind == MutableKeyword
                or parser.token.kind == SelfKeyword
            {
                // SelfParameter
                if parser.token.kind == MutableKeyword
                    { add_item(parameterChildren, expect_token(parser, MutableKeyword)); }

                add_item(parameterChildren, expect_token(parser, SelfKeyword));
                add_item(children, new Syntax_Node(SelfParameter, parameterChildren));
            }
            else
            {
                let mutableBinding: bool = parser.token.kind == VarKeyword;
                if parser.token.kind == VarKeyword
                    { add_item(parameterChildren, expect_token(parser, VarKeyword)); }
                add_item(parameterChildren, expect_token(parser, Identifier));
                add_item(parameterChildren, expect_token(parser, Colon));
                let type: Syntax_Node = parse_type(parser);
                add_item(parameterChildren, type);
                add_item(children, new Syntax_Node(Parameter, parameterChildren));
            }

            if parser.token.kind == Comma
                { add_item(children, expect_token(parser, Comma)); }
            else
                { break; }
        }
    }
    add_item(children, expect_token(parser, RightParen));
    return new Syntax_Node(ParameterList, children);
}

public parse_member_declaration(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();

    if parser.token.kind == PublicKeyword
        or parser.token.kind == ProtectedKeyword
        or parser.token.kind == InternalKeyword
        or parser.token.kind == PrivateKeyword
    {
        add_item(children, accept_token(parser));
    }
    else
    {
        // Really we expect an access modifier
        add_item(children, expect_token(parser, PublicKeyword));
    }

    if parser.token.kind == NewKeyword
    {
        // Constructor
        add_item(children, expect_token(parser, NewKeyword));
        if parser.token.kind == Identifier
            { add_item(children, expect_token(parser, Identifier)); }
        add_item(children, parse_parameter_list(parser));
        add_item(children, parse_block(parser));
        return new Syntax_Node(ConstructorDeclaration, children);
    }

    if parser.token.kind == VarKeyword
        or parser.token.kind == LetKeyword
    {
        add_item(children, parse_variable_declaration(parser, false));
        add_item(children, expect_token(parser, Semicolon));
        return new Syntax_Node(FieldDeclaration, children);
    }

    // Method Declaration
    add_item(children, expect_token(parser, Identifier));
    add_item(children, parse_parameter_list(parser));
    add_item(children, expect_token(parser, Arrow));
    add_item(children, parse_type(parser));
    add_item(children, parse_block(parser));
    return new Syntax_Node(MethodDeclaration, children);
}

public parse_declaration(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();

    if parser.token.kind == PublicKeyword
        or parser.token.kind == ProtectedKeyword
        or parser.token.kind == InternalKeyword
        or parser.token.kind == PrivateKeyword
    {
        add_item(children, accept_token(parser));
    }
    else
    {
        // TODO Really we expect an access modifier
        add_item(children, expect_token(parser, PublicKeyword));
    }

    if parser.token.kind == VarKeyword
        or parser.token.kind == LetKeyword
    {
        add_item(children, parse_variable_declaration(parser, true));
        add_item(children, expect_token(parser, Semicolon));
        return new Syntax_Node(GlobalDeclaration, children);
    }

    if parser.token.kind == ClassKeyword
    {
        add_item(children, expect_token(parser, ClassKeyword));
        add_item(children, expect_token(parser, Identifier));
        add_item(children, expect_token(parser, LeftBrace));
        while parser.token.kind =/= RightBrace and parser.token.kind =/= EndOfFileToken
        {
            let startToken: Syntax_Node = parser.token;
            add_item(children, parse_member_declaration(parser));
            if parser.token == startToken
            {
                // We have not advanced at all when trying to parse a member.
                // Skip a token to try to see if we can find the start of a member.
                add_item(children, new Syntax_Node.skipped(accept_token(parser)));
            }
        }
        add_item(children, expect_token(parser, RightBrace));
        return new Syntax_Node(ClassDeclaration, children);
    }

    if parser.token.kind == StructKeyword
    {
        add_item(children, expect_token(parser, StructKeyword));
        add_item(children, expect_token(parser, Identifier));
        add_item(children, expect_token(parser, LeftBrace));
        while parser.token.kind =/= RightBrace and parser.token.kind =/= EndOfFileToken
        {
            let startToken: Syntax_Node = parser.token;
            add_item(children, parse_member_declaration(parser));
            if parser.token == startToken
            {
                // We have not advanced at all when trying to parse a member.
                // Skip a token to try to see if we can find the start of a member.
                add_item(children, new Syntax_Node.skipped(accept_token(parser)));
            }
        }
        add_item(children, expect_token(parser, RightBrace));
        return new Syntax_Node(StructDeclaration, children);
    }

    if parser.token.kind == EnumKeyword
    {
        add_item(children, expect_token(parser, EnumKeyword));
        add_item(children, expect_token(parser, StructKeyword));
        add_item(children, expect_token(parser, Identifier));
        add_item(children, expect_token(parser, LeftBrace));

        while parser.token.kind =/= RightBrace and parser.token.kind =/= EndOfFileToken
        {
            let memberChildren: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
            add_item(memberChildren, expect_token(parser, Identifier));
            if parser.token.kind == Equals
            {
                add_item(memberChildren, expect_token(parser, Equals));
                add_item(memberChildren, expect_token(parser, Number));
            }
            if parser.token.kind =/= RightBrace
            {
                add_item(memberChildren, expect_token(parser, Comma));
            }
            add_item(children, new Syntax_Node(EnumMemberDeclaration, memberChildren));
        }

        add_item(children, expect_token(parser, RightBrace));
        return new Syntax_Node(EnumDeclaration, children);
    }

    // Function Declaration
    add_item(children, expect_token(parser, Identifier));
    add_item(children, parse_parameter_list(parser));
    add_item(children, expect_token(parser, Arrow));
    add_item(children, parse_type(parser));
    add_item(children, parse_block(parser));
    return new Syntax_Node(FunctionDeclaration, children);
}

public parse_compilation_unit(parser: mut Compilation_Unit_Parser) -> Syntax_Node
{
    let children: mut system.collections.List<Syntax_Node> = new system.collections.List<Syntax_Node>();
    while parser.token =/= none and parser.token.kind =/= EndOfFileToken
    {
        let startToken: Syntax_Node = parser.token;
        add_item(children, parse_declaration(parser));
        if parser.token == startToken
        {
            // We have not advanced at all when trying to parse a declaration.
            // Skip a token to try to see if we can find the start of a declaration.
            add_item(children, new Syntax_Node.skipped(accept_token(parser)));
        }
    }
    add_item(children, expect_token(parser, EndOfFileToken));
    return new Syntax_Node(CompilationUnit, children);
}
