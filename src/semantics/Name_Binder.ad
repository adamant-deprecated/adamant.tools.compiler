public class Name_Binder
{
	public bind(self, package: Package) -> void
	{
		let global_scope: Binding_Scope = new Binding_Scope(package.symbol.Children, package.PrimitiveTypes);
		for let compilation_unit: mut Semantic_Node in package.CompilationUnits
		{
			bind(compilation_unit, global_scope);
		}
	}

	private bind(self, node: mut Semantic_Node, scope: Binding_Scope) -> void
	{
		if node.Type == CompilationUnit
		{
			for let child: mut Semantic_Node in node.Children
			{
				bind(child, scope);
			}
		}
		else if node.Type == FunctionDeclaration
			or node.Type == MethodDeclaration
		{
			let parameters: mut Semantic_Node = node.FirstChildOfType(ParameterList);
			bind(parameters, scope);

			let return_type: mut Semantic_Node = node.Children[4];
			bind_type(return_type, scope);

			let function_scope: Binding_Scope = new Binding_Scope(scope, node.symbol.Children);
			let body: mut Semantic_Node = node.FirstChildOfType(Block);
			bind(body, function_scope);
		}
		else if node.Type == ParameterList
		{
			for let parameter: mut Semantic_Node in node.ChildrenOfType(Parameter)
			{
				bind(parameter, scope);
			}
		}
		else if node.Type == Parameter
		{
			let type: mut Semantic_Node = node.Children[|node.Children| - 1];
			bind_type(type, scope);
		}
		else if node.Type == ClassDeclaration
			or node.Type == StructDeclaration
		{
			let type_scope: Binding_Scope = new Binding_Scope(scope, node.symbol.Children);
			for let member: mut Semantic_Node in node.Members()
			{
				bind(member, type_scope);
			}
		}
		else if node.Type == FieldDeclaration
			or node.Type == GlobalDeclaration
		{
			let variable_declaration: mut Semantic_Node = node.FirstChildOfType(VariableDeclaration);
			bind(variable_declaration, scope);

		}
		else if node.Type == ConstructorDeclaration
		{
			let parameters: mut Semantic_Node = node.FirstChildOfType(ParameterList);
			bind(parameters, scope);

			let constructor_scope: Binding_Scope = new Binding_Scope(scope, node.symbol.Children);
			let body: mut Semantic_Node = node.FirstChildOfType(Block);
			bind(body, constructor_scope);
		}
		else if node.Type == VariableDeclaration
		{
			let type: mut Semantic_Node = node.Children[3];
			bind_type(type, scope);
			if |node.Children| == 6
			{
				let initalizer: mut Semantic_Node = node.Children[5];
				bind(initalizer, scope);
			}
		}
		else if node.Type == EnumDeclaration
		{
			// Haven't yet implemented anything in enums that requires name binding
		}

		else if node.Type == Block
		{
			for let statement: mut Semantic_Node in node.Statements()
			{
				bind(statement, scope);
			}
		}
		else if node.Type == LoopStatement
		{
			bind(node.FirstChildOfType(Block), scope);
		}
		else if node.Type == WhileStatement
		{
			bind(node.Children[1], scope); // condition expression
			bind(node.FirstChildOfType(Block), scope);
		}
		else if node.Type == DoWhileStatement
		{
			bind(node.FirstChildOfType(Block), scope);
			bind(node.Children[3], scope); // condition expression
		}
		else if node.Type == ForStatement
		{
			bind(node.FirstChildOfType(VariableDeclaration), scope);
			bind(node.FirstChildOfType(Block), scope);
		}
		else if node.Type == LocalDeclarationStatement
		{
			bind(node.FirstChildOfType(VariableDeclaration), scope);
		}
		else if node.Type == IfStatement
		{
			bind(node.Children[1], scope); // condition expression
			bind(node.FirstChildOfType(Block), scope);
			let elseClause: mut Semantic_Node? = node.FirstChildOfType(ElseClause);
			if elseClause =/= none
			{
				bind(elseClause, scope);
			}
		}
		else if node.Type == ElseClause
		{
			// This might be a good place for some kind of coalesce operator
			let block: mut Semantic_Node? = node.FirstChildOfType(Block);
			if block =/= none
			{
				bind(block, scope);
			}
			else
			{
				bind(node.FirstChildOfType(IfStatement), scope);
			}
		}
		else if node.Type == ExpressionStatement
		{
			bind(node.Children[0], scope);
		}
		else if node.Type == ReturnStatement
		{
			let child: mut Semantic_Node = node.Children[1];
			if child.Type =/= Semicolon
				{ bind(child, scope); }
		}
		else if node.Type == NewExpression
		{
			bind_type(node.Children[1], scope); // Type
			bind(node.Children[2], scope); // Call Arguments
		}
		else if node.Type == NotExpression
			or node.Type == ParenthesizedExpression
			or node.Type == MagnitudeExpression
			or node.Type == NegateExpression
		{
			bind(node.Children[1], scope);
		}
		else if node.Type == AssignmentExpression
			or node.Type == AndExpression
			or node.Type == OrExpression
			or node.Type == EqualExpression
			or node.Type == NotEqualExpression
			or node.Type == ComparisonExpression
			or node.Type == AddExpression
			or node.Type == SubtractExpression
			or node.Type == ElementAccessExpression
			or node.Type == RemainderExpression
			or node.Type == MultiplyExpression
			or node.Type == DivideExpression
		{
			bind(node.Children[0], scope); // lhs
			bind(node.Children[2], scope); // rhs
		}
		else if node.Type == MemberAccessExpression
		{
			bind(node.Children[0], scope); // Before dot
			// TODO bind member, need type of expression to left
		}
		else if node.Type == InvocationExpression
		{
			bind(node.Children[0], scope); // Thing Being Invoked
			bind(node.Children[1], scope); // Argument List
		}
		else if node.Type == ArgumentList
		{
			for let child: mut Semantic_Node in node.Children
			{
				if child.Type =/= LeftParen
					and child.Type =/= Comma
					and child.Type =/= RightParen
				{
					bind(child, scope);
				}
			}
		}
		else if node.Type == IdentifierName
		{
			if node.IsMissing { return; }
			let name: string = node.GetText();
			let symbol: Symbol = scope.Lookup(name);
			if symbol == none
			{
				add_resolution_error(node);
			}
			else
			{
				node.BindSymbol(symbol);
			}
		}
		else if node.Type == NumericLiteralExpression
			or node.Type == BreakStatement
			or node.Type == ContinueStatement
			or node.Type == NoneLiteralExpression
			or node.Type == SelfExpression
			or node.Type == TrueLiteralExpression
			or node.Type == FalseLiteralExpression
			or node.Type == StringLiteralExpression
			or node.Type == CodePointLiteralExpression
			or node.Type == EndOfFileToken
		{
			// Ignore these nodes
		}
		else
		{
			THROW_EXCEPTION("`Name_Binder.bind()` unimplemented node type " + node.Type);
		}
	}

	private bind_type(self, node: mut Semantic_Node, scope: Binding_Scope) -> void
	{
		if node.Type == PredefinedType
		{
			let name: string = node.Children[0].GetText();
			let symbol: Symbol = scope.GetPrimitive(name);
			if symbol == none { THROW_EXCEPTION("`Name_Binder.bind_type()` no symbol for primitive type " + name); }
			node.BindSymbol(symbol);
		}
		else if node.Type == MutableType
		{
			let innerType: mut Semantic_Node = node.Children[1];
			bind_type(innerType, scope);
		}
		else if node.Type == OptionalType
		{
			let innerType: mut Semantic_Node = node.Children[0];
			bind_type(innerType, scope);
		}
		else if node.Type == QualifiedName
		{
			let qualifier: mut Semantic_Node = node.Children[0];
			bind_type(qualifier, scope);
			// TODO rather than throwing an exception. We should say we can't resolve the subsequent identifiers
			if qualifier.symbol == none { THROW_EXCEPTION("`Name_Binder.bind_type()` no symbol for qualifier " + qualifier.GetText()); }
			let name: mut Semantic_Node = node.Children[2];
			if name.Type == IdentifierName
			{
				let symbol: Symbol = qualifier.symbol.Get(name.GetText(), IdentifierSymbol);
				name.BindSymbol(symbol);
				node.BindSymbol(symbol); // Set it on the name as a whole, not just the identifier
			}
			else if name.Type == GenericName
			{
				let identiferName: mut Semantic_Node = name.FirstChildOfType(IdentifierName);
				let symbol: Symbol = qualifier.symbol.Get(identiferName.GetText(), IdentifierSymbol);
				identiferName.BindSymbol(symbol);
				node.BindSymbol(symbol); // Set it on the name as a whole, not just the identifier

				// TODO need way to get the generic arguments
				var typeArg: bool = false;
				for let typeArgument: mut Semantic_Node in name.Children
				{
					if typeArg
					{
						if typeArgument.Type =/= GreaterThan
						{
							bind_type(typeArgument, scope);
						}
					}
					else if typeArgument.Type == LessThan
					{
						typeArg = true;
					}
				}
			}
			else
			{
				THROW_EXCEPTION("Unreachable: `Name_Binder.bind(..)` name.Type = " + name.Type);
			}
		}
		else if node.Type == IdentifierName
		{
			if node.IsMissing { return; }
			let name: string = node.GetText();
			let symbol: Symbol = scope.Lookup(name); // TODO this should be lookup type. Types are separate
			if symbol == none
			{
				add_resolution_error(node);
			}
			else
			{
				node.BindSymbol(symbol);
			}
		}
		else
		{
			THROW_EXCEPTION("`Name_Binder.bind_type()` unimplemented node type " + node.Type);
		}
	}

	private add_resolution_error(node: Semantic_Node) -> void
	{
		node.Add(new Diagnostic(FatalCompilationError, Analysis, node.Source, node.get_text_span(), "Could not resolve name `" + node.GetText() + "`"));
	}
}
