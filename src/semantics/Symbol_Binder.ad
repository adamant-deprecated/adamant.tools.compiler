public class Symbol_Binder
{
	public bind(self, package: Package) -> void
	{
		let global_scope: Binding_Scope = new Binding_Scope(package.symbol.children, package.PrimitiveTypes);
		for let compilation_unit: mut Semantic_Node in package.CompilationUnits
		{
			bind(compilation_unit, global_scope);
		}
	}

	private bind(self, node: mut Semantic_Node, scope: Binding_Scope) -> void
	{
		if node.kind == CompilationUnit
		{
			for let child: mut Semantic_Node in node.children
			{
				bind(child, scope);
			}
		}
		else if node.kind == FunctionDeclaration
			or node.kind == MethodDeclaration
		{
			let parameters: mut Semantic_Node = node.first_child(ParameterList);
			bind(parameters, scope);

			let return_type: mut Semantic_Node = node.children[4];
			bind_type_name(return_type, scope);

			let function_scope: Binding_Scope = new Binding_Scope(scope, node.symbol.children);
			let body: mut Semantic_Node = node.first_child(Block);
			bind(body, function_scope);
		}
		else if node.kind == ParameterList
		{
			for let parameter: mut Semantic_Node in node.children_of_kind(Parameter)
			{
				bind(parameter, scope);
			}
		}
		else if node.kind == Parameter
		{
			let type: mut Semantic_Node = node.children[|node.children| - 1];
			bind_type_name(type, scope);
		}
		else if node.kind == ClassDeclaration
			or node.kind == StructDeclaration
		{
			let type_scope: Binding_Scope = new Binding_Scope(scope, node.symbol.children);
			for let member: mut Semantic_Node in node.Members()
			{
				bind(member, type_scope);
			}
		}
		else if node.kind == FieldDeclaration
			or node.kind == GlobalDeclaration
		{
			let variable_declaration: mut Semantic_Node = node.first_child(VariableDeclaration);
			bind(variable_declaration, scope);

		}
		else if node.kind == ConstructorDeclaration
		{
			let parameters: mut Semantic_Node = node.first_child(ParameterList);
			bind(parameters, scope);

			let constructor_scope: Binding_Scope = new Binding_Scope(scope, node.symbol.children);
			let body: mut Semantic_Node = node.first_child(Block);
			bind(body, constructor_scope);
		}
		else if node.kind == VariableDeclaration
		{
			let identifier: mut Semantic_Node = node.first_child(Identifier);
			assert(identifier =/= none);
			let type_name: mut Semantic_Node = node.children[3];
			bind_type_name(type_name, scope);
			if |node.children| == 6
			{
				let initalizer: mut Semantic_Node = node.children[5];
				bind(initalizer, scope);
			}
			identifier.bind_type(type_name.type);
			node.bind_type(type_name.type);
		}
		else if node.kind == EnumDeclaration
		{
			// Haven't yet implemented anything in enums that requires name binding
		}

		else if node.kind == Block
		{
			for let statement: mut Semantic_Node in node.Statements()
			{
				bind(statement, scope);
			}
		}
		else if node.kind == LoopStatement
		{
			bind(node.first_child(Block), scope);
		}
		else if node.kind == WhileStatement
		{
			bind(node.children[1], scope); // condition expression
			bind(node.first_child(Block), scope);
		}
		else if node.kind == DoWhileStatement
		{
			bind(node.first_child(Block), scope);
			bind(node.children[3], scope); // condition expression
		}
		else if node.kind == ForStatement
		{
			bind(node.first_child(VariableDeclaration), scope);
			bind(node.first_child(Block), scope);
		}
		else if node.kind == LocalDeclarationStatement
		{
			let variable_declaration: mut Semantic_Node = node.first_child(VariableDeclaration);
			bind(variable_declaration, scope);
			node.bind_type(variable_declaration.type);
		}
		else if node.kind == IfStatement
		{
			bind(node.children[1], scope); // condition expression
			bind(node.first_child(Block), scope);
			let elseClause: mut Semantic_Node? = node.first_child(ElseClause);
			if elseClause =/= none
			{
				bind(elseClause, scope);
			}
		}
		else if node.kind == ElseClause
		{
			// This might be a good place for some kind of coalesce operator
			let block: mut Semantic_Node? = node.first_child(Block);
			if block =/= none
			{
				bind(block, scope);
			}
			else
			{
				bind(node.first_child(IfStatement), scope);
			}
		}
		else if node.kind == ExpressionStatement
		{
			bind(node.children[0], scope);
		}
		else if node.kind == ReturnStatement
		{
			let child: mut Semantic_Node = node.children[1];
			if child.kind =/= Semicolon
				{ bind(child, scope); }
		}
		else if node.kind == NewExpression
		{
			bind_type_name(node.children[1], scope); // Type
			bind(node.children[2], scope); // Call Arguments
		}
		else if node.kind == NotExpression
			or node.kind == ParenthesizedExpression
			or node.kind == MagnitudeExpression
			or node.kind == NegateExpression
		{
			bind(node.children[1], scope);
		}
		else if node.kind == AssignmentExpression
			or node.kind == AndExpression
			or node.kind == OrExpression
			or node.kind == EqualExpression
			or node.kind == NotEqualExpression
			or node.kind == ComparisonExpression
			or node.kind == AddExpression
			or node.kind == SubtractExpression
			or node.kind == ElementAccessExpression
			or node.kind == RemainderExpression
			or node.kind == MultiplyExpression
			or node.kind == DivideExpression
		{
			bind(node.children[0], scope); // lhs
			bind(node.children[2], scope); // rhs
		}
		else if node.kind == MemberAccessExpression
		{
			bind(node.children[0], scope); // Before dot
			// TODO bind member, need type of expression to left
		}
		else if node.kind == InvocationExpression
		{
			bind(node.children[0], scope); // Thing Being Invoked
			bind(node.children[1], scope); // Argument List
		}
		else if node.kind == ArgumentList
		{
			for let child: mut Semantic_Node in node.children
			{
				if child.kind =/= LeftParen
					and child.kind =/= Comma
					and child.kind =/= RightParen
				{
					bind(child, scope);
				}
			}
		}
		else if node.kind == IdentifierName
		{
			if node.is_missing { return; }
			let name: string = node.get_text();
			let symbol: Symbol = scope.lookup(name);
			if symbol == none
			{
				add_resolution_error(node);
			}
			else
			{
				node.bind_symbol(symbol);
				let type: Type? = symbol.get_type();
				if type =/= none
					{ node.bind_type(type); }
			}
		}
		else if node.kind == TrueLiteralExpression
			or node.kind == FalseLiteralExpression
		{
			node.bind_type(scope.lookup_primitive("bool").get_type());
		}
		else if node.kind == NumericLiteralExpression
			or node.kind == BreakStatement
			or node.kind == ContinueStatement
			or node.kind == NoneLiteralExpression
			or node.kind == SelfExpression
			or node.kind == StringLiteralExpression
			or node.kind == CodePointLiteralExpression
			or node.kind == EndOfFileToken
		{
			// Ignore these nodes
		}
		else
		{
			THROW_EXCEPTION("`Symbol_Binder.bind()` unimplemented node type " + node.kind);
		}
	}

	private bind_type_name(self, node: mut Semantic_Node, scope: Binding_Scope) -> void
	{
		if node.kind == PredefinedType
		{
			let primitive_name: string = node.children[0].get_text();
			let symbol: Symbol = scope.lookup_primitive(primitive_name);
			if symbol == none { THROW_EXCEPTION("`Symbol_Binder.bind_type_name()` no symbol for primitive type " + primitive_name); }
			node.bind_symbol(symbol);
			node.bind_type(new Type(symbol));
		}
		else if node.kind == MutableType
		{
			let inner_type_node: mut Semantic_Node = node.children[1];
			bind_type_name(inner_type_node, scope);
			if inner_type_node.type == none { THROW_EXCEPTION("`Symbol_Binder.bind_type_name()` no inner type for `"+node.get_text()+"` of kind `MutableType`"); }
			node.bind_type(inner_type_node.type.make_mutable());
		}
		else if node.kind == ImmutableType
		{
			let inner_type_node: mut Semantic_Node = node.children[0];
			bind_type_name(inner_type_node, scope);
			if inner_type_node.type == none { THROW_EXCEPTION("`Symbol_Binder.bind_type_name()` no inner type for `"+node.get_text()+"` of kind `ImmutableType`"); }
			node.bind_type(inner_type_node.type.make_immutable());
		}
		else if node.kind == OptionalType
		{
			let inner_type_node: mut Semantic_Node = node.children[0];
			bind_type_name(inner_type_node, scope);
			if inner_type_node.type == none { THROW_EXCEPTION("`Symbol_Binder.bind_type_name()` not inner type for `"+node.get_text()+"` of kind `OptionalType`"); }
			node.bind_type(inner_type_node.type.make_immutable());
		}
		else if node.kind == QualifiedName
		{
			let qualifier: mut Semantic_Node = node.children[0];
			bind_type_name(qualifier, scope);

			let name: mut Semantic_Node = node.children[2];
			if qualifier.symbol == none or qualifier.type == none
			{
				add_resolution_error(name);
			}
			else if name.kind == IdentifierName
			{
				let symbol: Symbol = qualifier.symbol.Get(name.get_text(), IdentifierSymbol);
				if symbol == none
				{
					add_resolution_error(name);
				}
				else
				{
					name.bind_symbol(symbol);
					name.bind_type(symbol.get_type());
					node.bind_type(symbol.get_type());
					// TODO transition away from using symbols at higher levels, use types instead
					node.bind_symbol(symbol); // Set it on the name as a whole, not just the identifier
				}
			}
			else if name.kind == GenericName
			{
				let identifer_name: mut Semantic_Node = name.first_child(IdentifierName);
				let symbol: Symbol = qualifier.symbol.Get(identifer_name.get_text(), IdentifierSymbol);
				if symbol == none
				{
					add_resolution_error(name);
					return;
				}
				identifer_name.bind_symbol(symbol);
				identifer_name.bind_type(symbol.get_type());
				// TODO transition away from using symbols at higher levels, use types instead
				node.bind_symbol(symbol); // Set it on the name as a whole, not just the identifier

				// TODO need way to get the generic type arguments
				var type_arg: bool = false;
				for let type_argument: mut Semantic_Node in name.children
				{
					if type_arg
					{
						if type_argument.kind =/= GreaterThan
						{
							bind_type_name(type_argument, scope);
						}
					}
					else if type_argument.kind == LessThan
					{
						type_arg = true;
					}
				}

				// TODO the type of the node should account for the generic type parameters
				node.bind_type(symbol.get_type()); // Set it on the name as a whole, not just the identifier
			}
			else
			{
				THROW_EXCEPTION("Unreachable: `Symbol_Binder.bind(..)` name.kind = " + name.kind);
			}
		}
		else if node.kind == IdentifierName
		{
			if node.is_missing { return; }
			let name: string = node.get_text();
			let symbol: Symbol = scope.lookup(name); // TODO this should be lookup type. Types are separate
			if symbol == none
			{
				add_resolution_error(node);
			}
			else
			{
				node.bind_symbol(symbol);
				node.bind_type(new Type(symbol));
			}
		}
		else
		{
			THROW_EXCEPTION("`Symbol_Binder.bind_type_name()` unimplemented node type " + node.kind);
		}
	}

	private add_resolution_error(node: Semantic_Node) -> void
	{
		node.Add(new Diagnostic(FatalCompilationError, Analysis, node.Source, node.get_text_span(), "Could not resolve name `" + node.get_text() + "`"));
	}
}
