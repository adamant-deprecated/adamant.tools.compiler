/// Builds up the tree of `Semantic_Node`s for all the compilation units
/// This class actually builds up the semantic tree for the package
public class Semantic_Tree_Builder
{
    public let optional_type_name: Name;

    public new()
    {
        let primitives_package: Package_Name = new Package_Name("$primitives");
        let global_namespace: Name = new Name.global_namespace(primitives_package);
        let adamant_namespace: Name = new Name(global_namespace, NamespaceName, "adamant");
        let language_namespace: Name = new Name(adamant_namespace, NamespaceName, "language");
        self.optional_type_name = new Name(language_namespace, TypeName, "optional");
    }
}

public build_compilation_units(
    builder: Semantic_Tree_Builder,
    package_syntax: Syntax_Node,
    name_table: Name_Table)
    -> system.collections.List<Compilation_Unit>
{
    assert(package_syntax.kind == PackageNode, "package_syntax.kind=" + int_to_string(package_syntax.kind));
    // TODO remove below assertion once this is fixed
    assert(lookup_special(name_table.any_package, "string") =/= none, "name_table.any_package.name="+full_name(name_table.any_package.name));

    let compilation_units: mut system.collections.List<Compilation_Unit> = new system.collections.List<Compilation_Unit>();

    for let compilation_unit_syntax: Syntax_Node in package_syntax.children
    {
        add_item(compilation_units, build_compilation_unit_semantic_node(builder, compilation_unit_syntax, name_table));
    }
    return compilation_units;
}

public build_compilation_unit_semantic_node(
    builder: Semantic_Tree_Builder,
    compilation_unit_syntax: Syntax_Node,
    name_table: Name_Table)
    -> Compilation_Unit
{
    assert(compilation_unit_syntax.kind == CompilationUnit, "compilation_unit_syntax.kind=" + int_to_string(compilation_unit_syntax.kind));

    // TODO we need to load the correct scope
    let declarations: mut system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();
    for let declaration_syntax: Syntax_Node in compilation_unit_syntax.children
    {
        add_item(declarations, build_semantic_node(builder, declaration_syntax, name_table, name_table.any_package));
    }
    return new Compilation_Unit(compilation_unit_syntax, declarations);
}

public build_semantic_node(
    builder: Semantic_Tree_Builder,
    syntax: Syntax_Node,
    name_table: Name_Table,
    scope: Name_Subtable)
    -> Semantic_Node
{
    let children: mut system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();

    if syntax.kind == FunctionDeclaration
        or syntax.kind == MethodDeclaration
    {
        add_item(children, build_semantic_node(builder, as_any(access_modifier(syntax)), name_table, scope));
        let function_name: Token = first_child_token(syntax, Identifier);
        add_item(children, build_semantic_node(builder, as_any(function_name), name_table, scope));

        let function_scope: mut Name_Subtable = find(scope, get_token_text(function_name));
        assert(function_scope =/= none, "get_syntax_node_text(function_name)="+get_token_text(function_name));

        let parameters: Syntax_Node = first_child_syntax_node(syntax, ParameterList);
        // TODO it is really only the parameters that are in this scope, the types aren't
        add_item(children, build_parameters_semantic_node(builder, parameters, name_table, function_scope));

        let return_type: Syntax_Node = syntax.children[4]; // TODO need more reliable way to pick this out
        add_item(children, build_type_name_semantic_node(builder, return_type, name_table, scope));

        let function_type: Type = function_type_as_type(new Function_Type(function_scope.name));
        // debug_write_line("Binding function `"+get_syntax_node_text(function_name)+"` with type `"+full_name(function_type.name)+"`, scope `"+full_name(function_scope.name)+"`");
        bind_type(function_scope, function_type);
        let body: Syntax_Node = first_child_syntax_node(syntax, Block);
        if body =/= none
        {
            add_item(children, build_semantic_node(builder, body, name_table, function_scope));
        }

        // TODO what about the function type?
        return new Semantic_Node.concrete_with_children(syntax, children);
    }
    else if syntax.kind == ClassDeclaration
        or syntax.kind == StructDeclaration
    {
        let type_name: Token = first_child_token(syntax, Identifier);
        add_item(children, build_semantic_node(builder, as_any(type_name), name_table, scope));
        let type_scope: Name_Subtable = find(scope, get_token_text(type_name));
        assert(type_scope =/= none, "get_syntax_node_text(type_name)="+get_token_text(type_name));

        for let member: Syntax_Node in members(syntax)
        {
            add_item(children, build_semantic_node(builder, member, name_table, type_scope));
        }

        // TODO what about the class/struct type?
        return new Semantic_Node.declares_type(type_scope.type, syntax, children);
    }
    else if syntax.kind == ConstructorDeclaration
    {
        add_item(children, build_semantic_node(builder, as_any(access_modifier(syntax)), name_table, scope));
        var full_name: string = "new";
        let constructor_name: Token? = first_child_token(syntax, Identifier);
        if constructor_name =/= none
        {
            full_name = "new_" + get_token_text(constructor_name);
            add_item(children, new Semantic_Node.token(constructor_name));
        }

        let constructor_scope: Name_Subtable = find_special(scope, full_name);
        assert(constructor_scope =/= none, "full_name="+full_name);

        let parameters: Syntax_Node = first_child_syntax_node(syntax, ParameterList);
        // TODO it is really only the parameters that are in this scope, the types aren't
        add_item(children, build_parameters_semantic_node(builder, parameters, name_table, constructor_scope));

        let body: Syntax_Node = first_child_syntax_node(syntax, Block);
        if body =/= none
        {
            add_item(children, build_semantic_node(builder, body, name_table, constructor_scope));
        }

        // TODO what about the function type?
        return new Semantic_Node.concrete_with_children(syntax, children);
    }
    else if syntax.kind == IdentifierName
    {
        let name_scope: Name_Subtable? = lookup(scope, get_syntax_node_text(syntax));
        if name_scope == none
        {
            if not syntax.is_missing
                { return new Semantic_Node.concrete(syntax, resolution_error(syntax)); }
            else
                { return new Semantic_Node.concrete(syntax); }
        }
        else
        {
            // let place: int = syntax.start;
            // debug_write_line("Building identifier `"+get_syntax_node_text(syntax)+"` at "+place+", scope `"+full_name(name_scope.name)+'`');
            // assert(name_scope.type =/= none, get_syntax_node_text(syntax));
            if name_scope.type == none
            {
                // TODO this case exists because of out of order declarations, fix it
                return new Semantic_Node.concrete_with_children(syntax, children);
            }
            else
            {
                return new Semantic_Node.of_type(name_scope.type, syntax, children);
            }
        }
    }
    else if syntax.kind == MemberAccessExpression
    {
        // Before dot
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        add_item(children, lhs);
        // assert(lhs.of_type =/= none, "Build Member Access: " + get_syntax_node_text(syntax));
        // After dot
        var rhs: Semantic_Node;
        let member_name: Syntax_Node = syntax.children[2];
        if lhs.of_type == none
        {
            rhs = new Semantic_Node.concrete(member_name);
            // TODO poison this item or something, we don't know what it is
        }
        else
        {
            // debug_write_line("Getting scope for type name `"+full_name(lhs.of_type.name)+"`");
            let access_scope: Name_Subtable = get_name(name_table, lhs.of_type.name);
            let member_scope: Name_Subtable? = find(access_scope, get_syntax_node_text(member_name));

            if member_scope == none
            {
                rhs = new Semantic_Node.concrete(member_name);
                // TODO this should be an error
                // resolution_error(rhs);
            }
            else
            {
                let no_children: system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();
                if member_scope.type == none
                {
                    // debug_write_line("Didn't find type for scope `"+full_name(member_scope.name)+"`");
                    rhs = new Semantic_Node.concrete(member_name);
                    // TODO this should be an error
                    // add_could_not_determine_type_error(rhs);
                }
                else
                {
                    rhs = new Semantic_Node.of_type(member_scope.type, member_name, no_children);
                }
            }
        }
        add_item(children, rhs);
        if rhs.of_type == none
        {
            return new Semantic_Node.concrete_with_children(syntax, children);
        }
        else
        {
            return new Semantic_Node.of_type(rhs.of_type, syntax, children);
        }
    }
    else if syntax.kind == NewExpression
    {
        add_item(children, build_constructor_name_semantic_node(builder, syntax.children[1], name_table, scope)); // Type
        add_item(children, build_semantic_node(builder, first_child_syntax_node(syntax, ArgumentList), name_table, scope)); // Call Arguments
        return new Semantic_Node.concrete_with_children(syntax, children);
    }
    else if syntax.kind == LocalDeclarationStatement
    {
        let variable_declaration_syntax: Syntax_Node = first_child_syntax_node(syntax, VariableDeclaration);
        add_item(children, build_semantic_node(builder, variable_declaration_syntax, name_table, scope));
        return new Semantic_Node.concrete_with_children(syntax, children);
    }
    else if syntax.kind == VariableDeclaration
    {
        let binding_syntax: Token = syntax.children[0]; // i.e. `let` or `var`
        add_item(children, new Semantic_Node.token(binding_syntax));
        let identifier: Token = first_child_token(syntax, Identifier);
        add_item(children, new Semantic_Node.token(identifier));
        let type_syntax: Syntax_Node = syntax.children[3];
        let type_node: Semantic_Node = build_type_name_semantic_node(builder, type_syntax, name_table, scope);
        assert(type_node.referenced_type =/= none, get_semantic_node_text(type_node));
        let variable_scope: mut Name_Subtable = lookup(scope, get_token_text(identifier));
        assert(variable_scope =/= none, get_syntax_node_text(syntax));
        // debug_write_line("Binding variable `"+get_syntax_node_text(identifier)+"` with type `"+full_name(type_node.referenced_type.name)+"`, full scope is `"+full_name(variable_scope.name)+"`");
        bind_type(variable_scope, type_node.referenced_type);
        // Now we have to do the same binding in the specific package instead of the 'any' package
        bind_type(as_mut(get_name(name_table, variable_scope.name)), type_node.referenced_type);
        add_item(children, type_node);
        if syntax.children.count == 6
        {
            let initalizer: Syntax_Node = syntax.children[5];
            add_item(children, build_semantic_node(builder, initalizer, name_table, scope));
        }
        return new Semantic_Node.concrete_with_children(syntax, children);
    }
    else if syntax.kind == MatchPattern
    {
        let identifier: Token = syntax.children[0]; // i.e. `let` or `var`
        add_item(children, new Semantic_Node.token(identifier));
        let colon: Token = syntax.children[1];
        add_item(children, new Semantic_Node.token(colon));
        let type_syntax: Syntax_Node = syntax.children[2];
        let type_node: Semantic_Node = build_type_name_semantic_node(builder, type_syntax, name_table, scope);
        // TODO set the type in the name table
        add_item(children, type_node);
        return new Semantic_Node.concrete_with_children(syntax, children);
    }
    else if syntax.kind == TrueLiteralExpression
        or syntax.kind == FalseLiteralExpression
    {
        // TODO rather than looking up the type, directly use the one in the package
        return new Semantic_Node.of_type(lookup_special(scope, "bool").type, syntax, children);
    }
    else if syntax.kind == StringLiteralExpression
    {
        // TODO rather than looking up the type, directly use the one in the package
        return new Semantic_Node.of_type(lookup_special(scope, "string").type, syntax, children);
    }
    else if syntax.kind == CodePointLiteralExpression
    {
        // TODO rather than looking up the type, directly use the one in the package
        return new Semantic_Node.of_type(lookup_special(scope, "code_point").type, syntax, children);
    }
    else if syntax.kind == IntegerLiteralExpression
    {
        // TODO rather than looking up the type, directly use the one in the package
        return new Semantic_Node.of_type(lookup_special(scope, "int").type, syntax, children);
    }
    else if syntax.kind == NoneLiteralExpression
    {
        // TODO rather than looking up the type, directly use the one in the package
        let type: Type = build_optional_type(builder, name_table, lookup_special(scope, "never").type);
        return new Semantic_Node.of_type(type, syntax, children);
    }
    else if syntax.kind == SelfExpression
    {
        add_item(children, new Semantic_Node.token(first_child_token(syntax, SelfKeyword)));
        return new Semantic_Node.of_type(lookup_special(scope, "self").type, syntax, children);
    }
    else if syntax.kind == NegateExpression
    {
        let operand: Semantic_Node = build_semantic_node(builder, syntax.children[1], name_table, scope);
        add_item(children, operand);
        assert(operand.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "int").type, syntax, children);
    }
    else if syntax.kind == AddExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        // assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        // assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        var optional_type: Type? = lhs.of_type;
        if optional_type == none { optional_type = rhs.of_type; }
        if optional_type == none { optional_type = lookup_special(scope, "int").type; }
        return new Semantic_Node.of_type(optional_type, syntax, children);
    }
    else if syntax.kind == SubtractExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        // assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "int").type, syntax, children);
    }
    else if syntax.kind == MultiplyExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "int").type, syntax, children);
    }
    else if syntax.kind == DivideExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "int").type, syntax, children);
    }
    else if syntax.kind == RemainderExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "int").type, syntax, children);
    }
    else if syntax.kind == ParenthesizedExpression
    {
        let operand: Semantic_Node = build_semantic_node(builder, syntax.children[1], name_table, scope);
        add_item(children, operand);
        assert(operand.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(operand.of_type, syntax, children);
    }
    else if syntax.kind == EqualExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        // assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        // assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "bool").type, syntax, children);
    }
    else if syntax.kind == NotEqualExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        // assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        // assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "bool").type, syntax, children);
    }
    else if syntax.kind == ComparisonExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, new Semantic_Node.token(syntax.children[1]));
        add_item(children, rhs);
        // assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        // assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "bool").type, syntax, children);
    }
    else if syntax.kind == AndExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        // assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        // assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "bool").type, syntax, children);
    }
    else if syntax.kind == OrExpression
    {
        let lhs: Semantic_Node = build_semantic_node(builder, syntax.children[0], name_table, scope);
        let rhs: Semantic_Node = build_semantic_node(builder, syntax.children[2], name_table, scope);
        add_item(children, lhs);
        add_item(children, rhs);
        // assert(lhs.of_type =/= none, get_syntax_node_text(syntax));
        // assert(rhs.of_type =/= none, get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "bool").type, syntax, children);
    }
    else if syntax.kind == NotExpression
    {
        let operand: Semantic_Node = build_semantic_node(builder, syntax.children[1], name_table, scope);
        add_item(children, operand);
        // assert(operand.of_type =/= none, "Build Not Expression: " + get_syntax_node_text(syntax));
        return new Semantic_Node.of_type(lookup_special(scope, "bool").type, syntax, children);
    }
    else if syntax.children.count == 0
    {
        // Create a semantic node to represent the token
        return new Semantic_Node.concrete(syntax);
    }
    else
    {
        // TODO this is a temporary measure to build up the tree.
        //      replace with specific constructions
        for let child_syntax: Syntax_Node in syntax.children
        {
            add_item(children, build_semantic_node(builder, child_syntax, name_table, scope));
        }
        return new Semantic_Node.concrete_with_children(syntax, children);
        // NOT_IMPLEMENTED("syntax.kind="+int_to_string(syntax.kind));
    }
}

public build_parameters_semantic_node(
    builder: Semantic_Tree_Builder,
    parameters_syntax: Syntax_Node,
    name_table: Name_Table,
    scope: Name_Subtable)
    -> mut Semantic_Node
{
    assert(parameters_syntax.kind == ParameterList, "parameters_syntax.kind="+ int_to_string(parameters_syntax.kind));
    let parameter_nodes: mut system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();
    for let parameter: Syntax_Node in parameters(parameters_syntax)
    {
        let children: mut system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();
        if parameter.kind == Parameter
        {
            let var_syntax: Token? = first_child_token(parameter, VarKeyword);
            if var_syntax =/= none
                { add_item(children, new Semantic_Node.token(var_syntax)); }
            let identifier: Token = first_child_token(parameter, Identifier);
            add_item(children, new Semantic_Node.token(identifier));
            // Because a parameter can have `var` at the beginning, the position of the type isn't constant
            let type_syntax: Syntax_Node = parameter.children[parameter.children.count - 1];
            let type_node: Semantic_Node = build_type_name_semantic_node(builder, type_syntax, name_table, scope);
            assert(type_node.referenced_type =/= none, get_syntax_node_text(parameter));
            add_item(children, type_node);
            let parameter_scope: mut Name_Subtable = lookup(scope, get_token_text(identifier));
            assert(parameter_scope =/= none, get_token_text(identifier));
            bind_type(parameter_scope, type_node.referenced_type);
            add_item(parameter_nodes, new Semantic_Node.of_type(type_node.referenced_type, parameter, children));
        }
        else if parameter.kind == SelfParameter
        {
            // TODO figure out the class type and call new Semantic_Node.of_type
            // For now, add all children, because we look for the `mut` keyword to see if self is mutable
            for let child_syntax: Syntax_Node in parameter.children
            {
                add_item(children, new Semantic_Node.concrete(child_syntax));
            }
            add_item(parameter_nodes, new Semantic_Node.concrete_with_children(parameter, children));
        }
        else
        {
            NOT_IMPLEMENTED("parameter.kind="+int_to_string(parameter.kind));
        }
    }

    return new Semantic_Node.concrete_with_children(parameters_syntax, parameter_nodes);
}

public build_type_name_semantic_node(
    builder: Semantic_Tree_Builder,
    syntax: Syntax_Node,
    name_table: Name_Table,
    scope: Name_Subtable)
    -> mut Semantic_Node
{
    let children: mut system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();

    if syntax.kind == PredefinedType
    {
        let primitive_name: string = get_syntax_node_text(syntax.children[0]);
        let primitive_entry: Name_Subtable? = lookup_special(scope, primitive_name); // TODO this should be lookup type. Types are separate
        assert(primitive_entry =/= none, "No entry for primitive `"+primitive_name+"`");
        return new Semantic_Node.referencing_type(primitive_entry.type, syntax, children);
    }
    else if syntax.kind == IdentifierName
    {
        if syntax.is_missing { return new Semantic_Node.concrete(syntax); }
        let name: string = get_syntax_node_text(syntax);
        let entry: Name_Subtable? = lookup(scope, name); // TODO this should be lookup type. Types are separate
        if entry == none
        {
            return new Semantic_Node.concrete(syntax, resolution_error(syntax));
        }
        else
        {
            return new Semantic_Node.referencing_type(entry.type, syntax, children);
        }
    }
    else if syntax.kind == QualifiedName
    {
        let qualifier_syntax: Syntax_Node = syntax.children[0];
        let qualifier: Semantic_Node = build_type_name_semantic_node(builder, qualifier_syntax, name_table, scope);
        add_item(children, qualifier);

        let qualified_syntax: Syntax_Node = syntax.children[2];
        // TODO there seems to be a lot of duplicate code in these cases
        if qualified_syntax.kind == IdentifierName
        {
            let name_syntax: Syntax_Node = qualified_syntax;

            if qualifier.referenced_type == none
            {
                // This means we aren't sure about the qualifier (i.e. type unknown)
                // add_could_not_determine_type_error(qualifier);

                let name_node: Semantic_Node = new Semantic_Node.concrete(name_syntax, resolution_error(name_syntax, qualifier));
                add_item(children, name_node);
                return new Semantic_Node.concrete_with_children(syntax, children);
            }
            else
            {
                let containing_type: Type = qualifier.referenced_type;
                let containing_scope: Name_Subtable = get_name(name_table, containing_type.name);
                // `containing_scope` shouldn't be none, because we looked it up to get the name
                assert(containing_scope =/= none, full_name(containing_type.name));
                let referenced_scope: Name_Subtable = find(containing_scope, get_syntax_node_text(name_syntax));

                if referenced_scope == none
                {
                    let name_node: Semantic_Node = new Semantic_Node.concrete(name_syntax, resolution_error(name_syntax, qualifier));
                    add_item(children, name_node);
                    return new Semantic_Node.concrete_with_children(syntax, children);
                }

                let referenced_type: Type = referenced_scope.type;
                let name_node: Semantic_Node = new Semantic_Node.referencing_type(referenced_type, name_syntax, new system.collections.List<Semantic_Node>());
                add_item(children, name_node);
                return new Semantic_Node.referencing_type(referenced_type, syntax, children);
            }
        }
        else if qualified_syntax.kind == GenericName
        {
            let name_syntax: Syntax_Node = first_child_syntax_node(qualified_syntax, IdentifierName);
            let generic_name_children: mut system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();

            if qualifier.referenced_type == none
            {
                // This means we aren't sure about the qualifier (i.e. type unknown)
                // add_could_not_determine_type_error(qualifier);

                let name_node: Semantic_Node = new Semantic_Node.concrete(name_syntax, resolution_error(name_syntax, qualifier));
                add_item(generic_name_children, name_node);
                build_type_arguments_semantic_node(builder, qualified_syntax, name_table, scope, generic_name_children);
                let qualified_name: mut Semantic_Node = new Semantic_Node.concrete_with_children(qualified_syntax, generic_name_children);
                add_item(children, qualified_name);
                return new Semantic_Node.concrete_with_children(syntax, children);
            }
            else
            {
                let containing_type: Type = qualifier.referenced_type;
                let containing_scope: Name_Subtable = get_name(name_table, containing_type.name);
                // `containing_scope` shouldn't be none, because we looked it up to get the name
                assert(containing_scope =/= none, full_name(containing_type.name));
                let referenced_scope: Name_Subtable = find(containing_scope, get_syntax_node_text(name_syntax));

                if referenced_scope == none
                {
                    let name_node: Semantic_Node = new Semantic_Node.concrete(name_syntax, resolution_error(name_syntax, qualifier));
                    add_item(generic_name_children, name_node);
                    build_type_arguments_semantic_node(builder, qualified_syntax, name_table, scope, generic_name_children);
                    let qualified_name: mut Semantic_Node = new Semantic_Node.concrete_with_children(qualified_syntax, generic_name_children);
                    add_item(children, qualified_name);
                    return new Semantic_Node.concrete_with_children(syntax, children);
                }

                let referenced_type_name: Type = referenced_scope.type;
                let name_node: Semantic_Node = new Semantic_Node.referencing_type(referenced_type_name, name_syntax, new system.collections.List<Semantic_Node>());
                add_item(generic_name_children, name_node);
                let type_arguments: system.collections.List<Type?> = build_type_arguments_semantic_node(builder, qualified_syntax, name_table, scope, generic_name_children);
                let referenced_type: Type = apply_generic_arguments(referenced_type_name, type_arguments);
                let qualified_name: mut Semantic_Node = new Semantic_Node.referencing_type(referenced_type, qualified_syntax, generic_name_children);
                add_item(children, qualified_name);
                return new Semantic_Node.referencing_type(referenced_type, syntax, children);
            }
        }
        else
        {
            UNREACHABLE();
        }

        UNREACHABLE();
    }
    else if syntax.kind == MutableType
    {
        let inner_type: Semantic_Node = build_type_name_semantic_node(builder, syntax.children[1], name_table, scope);
        add_item(children, inner_type);
        if inner_type.referenced_type == none
        {
            // add_could_not_determine_type_error(inner_type);
            return new Semantic_Node.concrete_with_children(syntax, children);
        }
        let type: Type = make_mutable_type(inner_type.referenced_type);
        return new Semantic_Node.referencing_type(type, syntax, children);
    }
    else if syntax.kind == ImmutableType
    {
        let inner_type: Semantic_Node = build_type_name_semantic_node(builder, syntax.children[0], name_table, scope);
        add_item(children, inner_type);
        if inner_type.referenced_type == none
        {
            // add_could_not_determine_type_error(inner_type);
            return new Semantic_Node.concrete_with_children(syntax, children);
        }
        let type: Type = make_immutable_type(inner_type.referenced_type);
        return new Semantic_Node.referencing_type(type, syntax, children);
    }
    else if syntax.kind == OptionalType
    {
        let inner_type: Semantic_Node = build_type_name_semantic_node(builder, syntax.children[0], name_table, scope);
        add_item(children, inner_type);
        if inner_type.referenced_type == none
        {
            // add_could_not_determine_type_error(inner_type);
            return new Semantic_Node.concrete_with_children(syntax, children);
        }
        let type: Type = build_optional_type(builder, name_table, inner_type.referenced_type);
        return new Semantic_Node.referencing_type(type, syntax, children);
    }
    else
    {
        // TODO this is a temporary measure to build up the tree.
        //      replace with specific constructions
        // for let child_syntax: Syntax_Node in syntax.children
        // {
        // 	add_item(children, build_type_name_semantic_node(builder, child_syntax, name_table, scope));
        // }
        // return new Semantic_Node.concrete_with_children(syntax, children);
        let place: int = syntax.start;
        NOT_IMPLEMENTED("syntax.kind="+int_to_string(syntax.kind)+" at "+int_to_string(place));
    }
}

public build_optional_type(
    builder: Semantic_Tree_Builder,
    name_table: Name_Table,
    base_type: Type
) -> Type
{
    let optional_type_scope: Name_Subtable? = get_name(name_table, builder.optional_type_name);
    assert(optional_type_scope =/= none);
    let optional_type: Type = optional_type_scope.type;
    let type_arguments: mut system.collections.List<Type?> = new system.collections.List<Type?>();
    add_item(type_arguments, base_type);
    let type: Type = apply_generic_arguments(optional_type, type_arguments);
    return type;
}

public build_type_arguments_semantic_node(
    builder: Semantic_Tree_Builder,
    syntax: Syntax_Node,
    name_table: Name_Table,
    scope: Name_Subtable,
    children: mut system.collections.List<Semantic_Node>)
    -> system.collections.List<Type>
{
    let arguments: mut system.collections.List<Type> = new system.collections.List<Type>();
    // for convenience, we ignore identifier names
    if syntax.kind == IdentifierName
        { return arguments; }

    for let type_argument: Syntax_Node in type_arguments(syntax)
    {
        let type_node: Semantic_Node = build_type_name_semantic_node(builder, type_argument, name_table, scope);
        add_item(arguments, type_node.referenced_type);
        add_item(children, type_node);
    }

    return arguments;
}

public build_constructor_name_semantic_node(
    builder: Semantic_Tree_Builder,
    syntax: Syntax_Node,
    name_table: Name_Table,
    scope: Name_Subtable)
    -> Semantic_Node
{
    if syntax.kind == QualifiedName
        and as_syntax_node(syntax.children[2]).kind == IdentifierName // as_syntax_node() needed for now for void_ptr from list
    {
        let children: mut system.collections.List<Semantic_Node> = new system.collections.List<Semantic_Node>();
        let qualifier: Syntax_Node = syntax.children[0];
        // It could be a named constructor call
        let type_node: Semantic_Node = build_type_name_semantic_node(builder, qualifier, name_table, scope);
        add_item(children, type_node);
        let name: Token = syntax.children[2];
        let constructor_name: string = "new_" + get_token_text(name);
        let referenced_type: Type = type_node.referenced_type;
        assert(referenced_type =/= none, "no referenced type for `"+get_semantic_node_text(type_node)+"`");
        let constructor_scope: Name_Subtable? = lookup_special(get_name(name_table, referenced_type.name), constructor_name);
        if constructor_scope =/= none
        {
            add_item(children, new Semantic_Node.token(name));
            // Need referenced name etc
            return new Semantic_Node.concrete_with_children(syntax, children);
        }
    }

    return build_type_name_semantic_node(builder, syntax, name_table, scope);
}

public as_syntax_node(syntax: Syntax_Node) -> Syntax_Node
{
    return syntax;
}

public resolution_error(node: Syntax_Node) -> Diagnostic
{
    return new Diagnostic(FatalCompilationError, Analysis, node.source, get_syntax_node_text_span(node), "Could not resolve name `" + get_syntax_node_text(node) + "`");
}

public resolution_error(node: Syntax_Node, qualifier: Semantic_Node) -> Diagnostic
{
    return new Diagnostic(FatalCompilationError, Analysis, node.source, get_syntax_node_text_span(node), "Could not resolve name `" + get_syntax_node_text(node) + "` because qualifier `" + get_semantic_node_text(qualifier) +"` could not be resolved.");
}

// public add_could_not_determine_type_error(node: mut Semantic_Node) -> void
// {
//     add_diagnostic(node, new Diagnostic(FatalCompilationError, Analysis, node.source, get_semantic_node_text_span(node), "Could not determine type for `" + get_semantic_node_text(node) + "`"));
// }
