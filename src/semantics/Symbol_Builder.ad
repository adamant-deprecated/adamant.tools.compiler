public class Symbol_Builder
{
	// Takes a package and builds the symbols for it
	public build_symbols(self, compilation_units: System.Collections.List<mut Semantic_Node>) -> Symbol
	{
		// TODO eventually the package symbol should have a name so that we can look up names in front of the package qualifier
		let package_symbol: mut Symbol = new Symbol(none, "", PackageSymbol);
		for let compilation_unit: mut Semantic_Node in compilation_units
		{
			if compilation_unit.kind =/= CompilationUnit
				{ THROW_EXCEPTION("`Symbol_Builder.build_symbols(...)` called with node of type " + compilation_unit.kind); }

			build_symbols(package_symbol, compilation_unit);
		}

		build_runtime_library_symbols(package_symbol);

		return package_symbol;
	}

	// Because a single call could produce multiple symbols, we pass the symbol collection in and add symbols to it.
	// Once we have generators, this would be a good place to use them.
	private build_symbols(self, parent: Symbol, node: mut Semantic_Node) -> void
	{
		if node.kind == CompilationUnit
		{
			for let declaration: mut Semantic_Node in node.children
			{
				build_symbols(parent, declaration);
			}
		}
		else if node.kind == FunctionDeclaration
			or node.kind == MethodDeclaration
		{
			let name: string = node.first_child(Identifier).get_text();
			let symbol: Symbol = new Symbol(parent, name);
			build_function_symbols(parent, node, symbol);
		}
		else if node.kind == ClassDeclaration
			or node.kind == StructDeclaration
		{
			let name: string = node.first_child(Identifier).get_text();
			let symbol: Symbol = new Symbol(parent, name);
			symbol.Declarations.Add(node);
			for let member: mut Semantic_Node in node.Members()
			{
				build_symbols(symbol, member);
			}

			parent.children.Add(symbol);
			node.bind_symbol(symbol);
		}
		else if node.kind == ConstructorDeclaration
		{
			let constructor_symbol: Symbol = new Symbol(none, "", ConstructorSymbol);
			// TODO create a special self symbol
			constructor_symbol.Declarations.Add(node);
			build_function_symbols(parent, node, constructor_symbol);
		}
		else if node.kind == FieldDeclaration
		{
			let name: string = node.first_child(VariableDeclaration).first_child(Identifier).get_text();
			let field_symbol: Symbol = new Symbol(none, name);
			field_symbol.Declarations.Add(node);
			parent.children.Add(field_symbol);
			node.bind_symbol(field_symbol);
		}
		else if node.kind == EnumDeclaration
		{
			let name: string = node.first_child(Identifier).get_text();
			let enum_symbol: Symbol = new Symbol(parent, name);
			enum_symbol.Declarations.Add(node);
			// TODO add enum member symbols
			parent.children.Add(enum_symbol);
			node.bind_symbol(enum_symbol);
		}
		else if node.kind == GlobalDeclaration
		{
			let name: string = node.first_child(VariableDeclaration).first_child(Identifier).get_text();
			let globalDeclarationSymbol: Symbol = new Symbol(none, name);
			globalDeclarationSymbol.Declarations.Add(node);
			parent.children.Add(globalDeclarationSymbol);
			node.bind_symbol(globalDeclarationSymbol);
		}
		else if node.kind == Block
		{
			for let statement: mut Semantic_Node in node.Statements()
			{
				build_symbols(parent, statement);
			}
		}
		else if node.kind == LoopStatement
			or node.kind == WhileStatement
			or node.kind == DoWhileStatement
		{
			build_symbols(parent, node.first_child(Block));
		}
		else if node.kind == ForStatement
		{
			let name: string = node.first_child(VariableDeclaration).first_child(Identifier).get_text();
			let for_symbol: Symbol = new Symbol(none, name);
			for_symbol.Declarations.Add(node);
			parent.children.Add(for_symbol);
			node.bind_symbol(for_symbol);
			build_symbols(parent, node.first_child(Block));
		}
		else if node.kind == LocalDeclarationStatement
		{
			let identifier: mut Semantic_Node = node.first_child(VariableDeclaration).first_child(Identifier);
			let symbol: Symbol = new Symbol(none, identifier.get_text());
			symbol.Declarations.Add(node);
			identifier.bind_symbol(symbol);
			parent.children.Add(symbol);
		}
		else if node.kind == IfStatement
		{
			build_symbols(parent, node.first_child(Block));
			let elseClause: mut Semantic_Node? = node.first_child(ElseClause);
			if elseClause =/= none
			{
				build_symbols(parent, elseClause);
			}
		}
		else if node.kind == ElseClause
		{
			// This might be a good place for some kind of coalesce operator
			let block: mut Semantic_Node? = node.first_child(Block);
			if block =/= none
			{
				build_symbols(parent, block);
			}
			else
			{
				build_symbols(parent, node.first_child(IfStatement));
			}
		}
		else if node.kind == Parameter
		{
			let name: string = node.first_child(Identifier).get_text();
			let symbol: Symbol = new Symbol(none, name);
			symbol.Declarations.Add(node);
			parent.children.Add(symbol);
		}
		else if node.kind == SelfParameter
		{
			let name: string = node.first_child(SelfKeyword).get_text();
			let symbol: Symbol = new Symbol(none, name, PrimitiveSymbol);
			symbol.Declarations.Add(node);
			parent.children.Add(symbol);
		}
		else if node.kind == ExpressionStatement
			or node.kind == ReturnStatement
			or node.kind == BreakStatement
			or node.kind == ContinueStatement
			or node.kind == EndOfFileToken
		{
			// Ignore these nodes
		}
		else
		{
			THROW_EXCEPTION("`Symbol_Builder.Build()` unimplemented node type " + node.kind);
		}
	}

	private build_function_symbols(self, parent: Symbol, node: mut Semantic_Node, symbol: Symbol) -> void
	{
		symbol.Declarations.Add(node);

		let parameters: mut Semantic_Node = node.first_child(ParameterList);
		for let parameter: mut Semantic_Node in parameters.Parameters()
		{
			build_symbols(symbol, parameter);
		}

		let body: mut Semantic_Node = node.first_child(Block);
		build_symbols(symbol, body);

		parent.children.Add(symbol);
		node.bind_symbol(symbol);
	}

	/// This builds up a set of symbols that are actually in the Runtime Library
	/// as if they were part of the current package.
	///
	/// TODO this should actually be a separate package
	private build_runtime_library_symbols(self, package: Symbol) -> void
	{
		build_resource_manager(package);

		build_function(package, "THROW_EXCEPTION");
		build_function(package, "UNIMPLEMENTED");
		build_function(package, "UNREACHABLE");
		build_function(package, "assert");

		let system: mut Symbol = build(package, "System", NamespaceDeclaration);

		let collections: mut Symbol = build(system, "Collections", NamespaceDeclaration);
		build(collections, "List", ClassDeclaration);

		let console: Symbol = build(system, "Console", NamespaceDeclaration);
		build(console, "Console", ClassDeclaration);
		build(console, "Arguments", ClassDeclaration);

		let io: Symbol = build(system, "IO", NamespaceDeclaration);
		build(io, "File_Reader", ClassDeclaration);
		build(io, "File_Writer", ClassDeclaration);

		let text: Symbol = build(system, "Text", NamespaceDeclaration);
		build(text, "String_Builder", ClassDeclaration);
	}

	private build_resource_manager(parent: Symbol) -> void
	{
		let class_symbol: Symbol = build(parent, "Resource_Manager", ClassDeclaration);
		let fake_declaration: mut Semantic_Node = new Semantic_Node(new Syntax_Node(GlobalDeclaration, true, none, 0, 0));
		let symbol: mut Symbol = new Symbol(parent, "resource_manager");
		symbol.Declarations.Add(fake_declaration);
		fake_declaration.bind_symbol(symbol);
		fake_declaration.bind_type(new Type(class_symbol));
		parent.children.Add(symbol);
	}

	private build(parent: Symbol, name: string, declaration_kind: int) -> mut Symbol
	{
		let fake_declaration: mut Semantic_Node = new Semantic_Node(new Syntax_Node(declaration_kind, true, none, 0, 0));
		let symbol: mut Symbol = new Symbol(parent, name);
		symbol.Declarations.Add(fake_declaration);
		fake_declaration.bind_symbol(symbol);
		fake_declaration.bind_type(new Type(symbol));
		parent.children.Add(symbol);
		return symbol;
	}

	private build_function(parent: Symbol, name: string) -> void
	{
		let fake_declaration: mut Semantic_Node = new Semantic_Node(new Syntax_Node(FunctionDeclaration, true, none, 0, 0));
		let symbol: mut Symbol = new Symbol(parent, name);
		symbol.Declarations.Add(fake_declaration);
		fake_declaration.bind_symbol(symbol);
		parent.children.Add(symbol);
	}
}
